/**
 *	UI module: ScoresTable server side
 */
#Const Version		"2021-07-06"
#Const ScriptName	"ManiaApps/Nadeo/TMNext/TrackMania/Royal/UIModules/ScoresTable_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/ModeLibs/Common/UIModules_Common.Script.txt" as UIModules_Common
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Royal/UIModules/ScoresTable_Common.Script.txt" as UIModules_ScoresTable_Common

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the id of the UI module
Text GetId() {
	return UIModules_ScoresTable_Common::C_Id;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the configuration of the UI module
UIModules_Common::ComponentModeLibsUIModules_K_ModuleConfig GetConfig() {
	return UIModules_ScoresTable_Common::C_UIModuleConfig;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Set the message that the player can copy
 *	to their clipboard by clicking on it
 *
 *	@param	_DisplayedMessage				The message that is displayed
 *	@param	_ClibpboardValue				The value that will be copied to the clipboard
 */
Void SetClipboardMessage(Text _DisplayedMessage, Text _ClipboardValue) {
	declare netwrite Text Net_Royal_ScoresTable_ClipboardDisplayedMessage for Teams[0];
	declare netwrite Text Net_Royal_ScoresTable_ClipboardValue for Teams[0];
	Net_Royal_ScoresTable_ClipboardDisplayedMessage = _DisplayedMessage;
	Net_Royal_ScoresTable_ClipboardValue = _ClipboardValue;
}
Void ResetClipboardMessage() {
	SetClipboardMessage("", "");
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Display a message in the scores table header
Void SetHeaderMessage(Text _Message) {
	declare netwrite Text Net_Royal_ScoresTable_HeaderMessage for Teams[0];
	Net_Royal_ScoresTable_HeaderMessage = _Message;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Display the map progression in the scores table header
Void SetMapsProgression(Integer _Current, Integer _Max) {
	declare netwrite Int2 Net_Royal_ScoresTable_MapsProgression for Teams[0];
	Net_Royal_ScoresTable_MapsProgression = <_Current, _Max>;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;
	declare CUIConfig UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;
	
	declare netwrite Boolean Net_Royal_ScoresTable_IsVisible for UI;
	Net_Royal_ScoresTable_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;
	
	SetVisibility(_Player, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	ResetClipboardMessage();
	SetHeaderMessage("");
	SetMapsProgression(0, 0);
	
	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}