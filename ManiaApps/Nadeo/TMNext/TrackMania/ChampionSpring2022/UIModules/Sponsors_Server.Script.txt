/**
 *	UI module: Sponsors server side
 */
#Const Version		"2022-05-31"
#Const ScriptName	"ManiaApps/Nadeo/TMNext/TrackMania/ChampionSpring2022/UIModules/Sponsors_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "TextLib" as TL
#Include "Libs/Nadeo/ModeLibs/Common/UIModules_Common.Script.txt" as UIModules_Common
#Include "ManiaApps/Nadeo/TMNext/TrackMania/ChampionSpring2022/UIModules/Sponsors_Common.Script.txt" as UIModules_Sponsors_Common

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the id of the UI module
Text GetId() {
	return UIModules_Sponsors_Common::C_Id;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the configuration of the UI module
UIModules_Common::ComponentModeLibsUIModules_K_ModuleConfig GetConfig() {
	return UIModules_Sponsors_Common::C_UIModuleConfig;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Send the sponsors URL to the clients
Void SetSponsorsURL(Text[] _SponsorsURL) {
	declare netwrite Text[] Net_ChampionSpring2022_Sponsors_SponsorsURL for Teams[0];
	if (
		_SponsorsURL.count != Net_ChampionSpring2022_Sponsors_SponsorsURL.count ||
		!_SponsorsURL.containsonly(Net_ChampionSpring2022_Sponsors_SponsorsURL)
	) {
		declare netwrite Integer Net_ChampionSpring2022_Sponsors_SponsorsURLUpdate for Teams[0];
		Net_ChampionSpring2022_Sponsors_SponsorsURL = _SponsorsURL;
		Net_ChampionSpring2022_Sponsors_SponsorsURLUpdate += 1;
	}
}
Void SetSponsorsURL(Text _SponsorsURL) {
	SetSponsorsURL(TL::Split(" ", _SponsorsURL));
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;
	declare CUIConfig UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;

	declare netwrite Boolean Net_ChampionSpring2022_Sponsors_IsVisible for UI;
	Net_ChampionSpring2022_Sponsors_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;
	
	SetVisibility(_Player, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	SetSponsorsURL([]);
	
	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}