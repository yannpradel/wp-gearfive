/**
 *  UI module: LapsCounter server side
 */
#Const Version    "2021-03-03"
#Const ScriptName "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/LapsCounter_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Id "UIModule_Champion_LapsCounter"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Send the best lap time
Void SetBestLapPlayer(Text _WebServicesUserId, Integer _Time, Boolean _IsWorldRecord, Boolean _IsInit) {
	declare netwrite Integer Net_Champion_LapsCounter_BestLapUpdate for Teams[0];
	declare netwrite Text Net_Champion_LapsCounter_BestLapPlayer for Teams[0];
	declare netwrite Integer Net_Champion_LapsCounter_BestLapTime for Teams[0];
	declare netwrite Boolean Net_Champion_LapsCounter_IsWorldRecord for Teams[0];
	declare netwrite Boolean Net_Champion_LapsCounter_IsInit for Teams[0];
	Net_Champion_LapsCounter_BestLapUpdate = Now;
	Net_Champion_LapsCounter_BestLapPlayer = _WebServicesUserId;
	Net_Champion_LapsCounter_BestLapTime = _Time;
	Net_Champion_LapsCounter_IsWorldRecord = _IsWorldRecord;
	Net_Champion_LapsCounter_IsInit = _IsInit;
}
Void SetBestLapPlayer(Text _WebServicesUserId, Integer _Time) {
	SetBestLapPlayer(_WebServicesUserId, _Time, False, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Display round or map number
Void SetRoundMode(Boolean _IsRoundMode) {
	declare netwrite Boolean Net_Champion_LapsCounter_IsRoundMode for Teams[0];
	Net_Champion_LapsCounter_IsRoundMode = _IsRoundMode;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;
	
	declare netwrite Boolean Net_Champion_LapsCounter_IsVisible for _Player;
	Net_Champion_LapsCounter_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;
	
	SetVisibility(_Player, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	SetBestLapPlayer("", 0);
	SetRoundMode(False);
	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}