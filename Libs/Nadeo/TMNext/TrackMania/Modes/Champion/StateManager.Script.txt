/** 
 *	State manager for the Champion mode
 */
#Const Version		"2021-01-14"
#Const ScriptName	"Libs/Nadeo/TMNext/TrackMania/Modes/Champion/StateManager.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Libraries
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/ModeLibs/Common/UIModules_Server.Script.txt" as UIModules

#Include "ManiaApps/Nadeo/TMxSM/Race/UIModules/Chrono_Server.Script.txt" as UIModules_Chrono_Default
#Include "ManiaApps/Nadeo/TMxSM/Race/UIModules/RespawnHelper_Server.Script.txt" as UIModules_RespawnHelper
#Include "ManiaApps/Nadeo/TMxSM/Race/UIModules/LapsCounter_Server.Script.txt" as UIModules_LapsCounter

#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/NetShareChampion_Server.Script.txt" as UIModules_NetShare_Champion
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/ChronoChampion_Server.Script.txt" as UIModules_Chrono_Champion
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/ScoresTable_Server.Script.txt" as UIModules_ScoresTable_Champion
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/LapsCounter_Server.Script.txt" as UIModules_LapsCounter_Champion
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/SpectatorInfo_Server.Script.txt" as UIModules_SpectatorInfo
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/PlayerStats_Server.Script.txt" as UIModules_PlayerStats
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/PausePopUp_Server.Script.txt" as UIModules_PausePopUp
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/CountDown_Server.Script.txt" as UIModules_CountDown
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/BestLap_Server.Script.txt" as UIModules_BestLap
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/TimeDiffToPoints_Server.Script.txt" as UIModules_TimeDiffToPoints
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/WarmUp_Server.Script.txt" as UIModules_WarmUp
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/MapInfo_Server.Script.txt" as UIModules_MapInfo
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/LiveRankingChampion_Server.Script.txt" as UIModules_LiveRankingChampion
#Include "ManiaApps/Nadeo/TMNext/TrackMania/Champion/UIModules/Sponsors_Server.Script.txt" as UIModules_Sponsors
#Include "ManiaApps/Nadeo/TMxSM/Race/UIModules/Countdown_Server.Script.txt" as UIModules_Countdown_Default
#Include "ManiaApps/Nadeo/TMxSM/Race/UIModules/ScoresTable_Server.Script.txt" as UIModules_ScoresTable_Default
#Include "ManiaApps/Nadeo/TMxSM/Race/UIModules/DisplayMessage_Server.Script.txt" as UIModules_DisplayMessage_Default
#Include "Libs/Nadeo/TMNext/TrackMania/Modes/ChampionCommon/Constants.Script.txt" as LibChampion_Constants

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/*	Enabling this setting will have
 *	a big impact on the script performances
 *	in crowded server. The library
 *	will do a check on every player
 *	of the server on every frame.
 *	If the script respect the convention
 *	to always use MB_Yield() instead of yield;
 *	then it should not have to enforce
 *	the state at each frame. Any new
 *	player will be initialized when
 *	they join the server
 */
#Const C_ForceStatesEveryFrame False

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Globales
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
declare Integer[] G_ForcedStates;

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
Integer[] GetForcedStates() {
	return G_ForcedStates;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Apply the given states to the given player
 *
 *	@param	_Player										The player to update
 *	@param	_States										The states to apply
 */
Void Private_Apply(CPlayer _Player, Integer[] _States) {
	if (_Player == Null) return;
	
	UIModules_LapsCounter_Champion::SetVisibility(_Player, False);
	UIModules_SpectatorInfo::SetVisibility(_Player, False);
	UIModules_Chrono_Champion::SetVisibility(_Player, False);
	UIModules_PlayerStats::SetVisibility(_Player, False);
	UIModules_PausePopUp::SetVisibility(_Player, False);
	UIModules_CountDown::SetVisibility(_Player, False);
	UIModules_BestLap::SetVisibility(_Player, False);
	UIModules_TimeDiffToPoints::SetVisibility(_Player, False);
	UIModules_WarmUp::SetVisibility(_Player, False);
	UIModules_MapInfo::SetVisibility(_Player, False);
	UIModules_LiveRankingChampion::SetVisibility(_Player, False);

	UIModules_ScoresTable_Champion::SetVisibility(_Player, True);
	UIModules_Sponsors::SetVisibility(_Player, True);
	
	foreach (State in _States) {
		switch (State) {
			case LibChampion_Constants::C_State_Playing: {
				UIModules_LapsCounter_Champion::SetVisibility(_Player, True);
				UIModules_SpectatorInfo::SetVisibility(_Player, True);
				UIModules_Chrono_Champion::SetVisibility(_Player, True);
				//UIModules_PlayerStats::SetVisibility(_Player, True); // Disable player statistics
				UIModules_CountDown::SetVisibility(_Player, True);
				//UIModules_BestLap::SetVisibility(_Player, True); // Disable best lap info
				UIModules_TimeDiffToPoints::SetVisibility(_Player, True);
				UIModules_LiveRankingChampion::SetVisibility(_Player, True);
			}
			case LibChampion_Constants::C_State_WarmUp: {
				UIModules_MapInfo::SetVisibility(_Player, True);
				UIModules_WarmUp::SetVisibility(_Player, True);
				UIModules_LiveRankingChampion::SetVisibility(_Player, True);
				UIModules_LapsCounter_Champion::SetVisibility(_Player, True);
			}
			case LibChampion_Constants::C_State_Pause: {
				UIModules_PausePopUp::SetVisibility(_Player, True);
			}
		}
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Add a state to a player
 *
 *	@param	_Player										The player to update
 *	@param	_State										The state to add
 */
Void Add(CPlayer _Player, Integer _State) {
	if (_Player == Null) return;
	declare Integer[] LibStateMgr_States for _Player;
	if (!LibStateMgr_States.exists(_State)) {
		LibStateMgr_States.add(_State);
		if (G_ForcedStates.count <= 0) {
			Private_Apply(_Player, LibStateMgr_States);
		}
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Remove a state from a player
 *
 *	@param	_Player										The player to update
 *	@param	_State										The state to remove
 */
Void Remove(CPlayer _Player, Integer _State) {
	if (_Player == Null) return;
	declare Integer[] LibStateMgr_States for _Player;
	if (LibStateMgr_States.exists(_State)) {
		declare Removed = LibStateMgr_States.remove(_State);
		if (G_ForcedStates.count <= 0) {
			Private_Apply(_Player, LibStateMgr_States);
		}
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Remove all state from a player
 *
 *	@param	_Player										The player to reset
 */
Void Reset(CPlayer _Player) {
	if (_Player == Null) return;
	declare Integer[] LibStateMgr_States for _Player;
	LibStateMgr_States = [];
	if (G_ForcedStates.count <= 0) {
		Private_Apply(_Player, LibStateMgr_States);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Check if the player has the given state
 *
 *	@param	_Player										The player to check
 *	@param	_State										The state to find
 *
 *	@return														True if the player has the state
 *																		False otherwise
 */
Boolean HasState(CPlayer _Player, Integer _State) {
	if (_Player == Null) return False;

	declare Integer[] LibStateMgr_States for _Player;
	return LibStateMgr_States.exists(_State) || G_ForcedStates.exists(_State);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Force the states applied to all players
 *	To disable the forced states use an empty array
 *
 *	@param	_States									The states to force
 */
Void ForcePlayersStates(Integer[] _States) {
	G_ForcedStates = _States;
	
	foreach (Player in AllPlayers) {
		declare Boolean LibStateMgr_IsForcedStates for Player = False;
		// Enable
		if (G_ForcedStates.count > 0) {
			LibStateMgr_IsForcedStates = True;
			Private_Apply(Player, G_ForcedStates);
		}
		// Disable
		else {
			LibStateMgr_IsForcedStates = False;
			declare Integer[] LibStateMgr_States for Player;
			Private_Apply(Player, LibStateMgr_States);
		}
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update the library
Void Yield() {
	if (C_ForceStatesEveryFrame && G_ForcedStates.count > 0) {
		foreach (Player in AllPlayers) {
			declare Boolean LibStateMgr_IsForcedStates for Player = False;
			if (!LibStateMgr_IsForcedStates) {
				LibStateMgr_IsForcedStates = True;
				Private_Apply(Player, G_ForcedStates);
			}
		}
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;
	
	UIModules_ScoresTable_Champion::InitializePlayer(_Player);
	UIModules_LapsCounter_Champion::InitializePlayer(_Player);
	UIModules_SpectatorInfo::InitializePlayer(_Player);
	UIModules_Chrono_Champion::InitializePlayer(_Player);
	UIModules_PlayerStats::InitializePlayer(_Player);
	UIModules_PausePopUp::InitializePlayer(_Player);
	UIModules_CountDown::InitializePlayer(_Player);
	UIModules_BestLap::InitializePlayer(_Player);
	UIModules_TimeDiffToPoints::InitializePlayer(_Player);
	UIModules_WarmUp::InitializePlayer(_Player);
	UIModules_MapInfo::InitializePlayer(_Player);
	UIModules_LiveRankingChampion::InitializePlayer(_Player);
	UIModules_Sponsors::InitializePlayer(_Player);
	
	Reset(_Player);
	
	if (G_ForcedStates.count > 0) {
		declare Boolean LibStateMgr_IsForcedStates for _Player = False;
		LibStateMgr_IsForcedStates = True;
		Private_Apply(_Player, G_ForcedStates);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	ForcePlayersStates([]);
	
	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}

	UIModules_Sponsors::Unload();
	UIModules_LiveRankingChampion::Unload();
	UIModules_MapInfo::Unload();
	UIModules_ScoresTable_Champion::Unload();
	UIModules_WarmUp::Unload();
	UIModules_LapsCounter_Champion::Unload();
	UIModules_TimeDiffToPoints::Unload();
	UIModules_SpectatorInfo::Unload();
	UIModules_Chrono_Champion::Unload();
	UIModules_PausePopUp::Unload();
	UIModules_CountDown::Unload();
	UIModules_PlayerStats::Unload();
	UIModules_BestLap::Unload();
	UIModules_NetShare_Champion::Unload();
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
	
	UIModules_NetShare_Champion::Load();
	UIModules_BestLap::Load();
	UIModules_PlayerStats::Load();
	UIModules_CountDown::Load();
	UIModules_PausePopUp::Load();
	UIModules_Chrono_Champion::Load();
	UIModules_SpectatorInfo::Load();
	UIModules_TimeDiffToPoints::Load();
	UIModules_LapsCounter_Champion::Load();
	UIModules_WarmUp::Load();
	UIModules_ScoresTable_Champion::Load();
	UIModules_MapInfo::Load();
	UIModules_LiveRankingChampion::Load();
	UIModules_Sponsors::Load();
	
	/* @scaffolding ui_module_customizable */
	
	UIModules::LoadModules([ // charge les modules qu'on veut
		UIModules_NetShare_Champion::C_Id,
		UIModules_BestLap::C_Id,
		UIModules_PlayerStats::C_Id,
		UIModules_CountDown::C_Id,
		UIModules_PausePopUp::C_Id,
		UIModules_Chrono_Champion::C_Id,
		UIModules_SpectatorInfo::C_Id,
		UIModules_TimeDiffToPoints::C_Id,
		UIModules_LapsCounter_Champion::C_Id,
		UIModules_WarmUp::C_Id,
		UIModules_ScoresTable_Champion::C_Id,
		UIModules_MapInfo::C_Id,
		UIModules_LiveRankingChampion::C_Id,
		UIModules_Sponsors::C_Id
	]);

	UIModules::UnloadModules([ // enleve les modules par d√©faut qu'on ne veut pas
		UIModules_LapsCounter::GetId(),
		UIModules_Chrono_Default::GetId(),
		UIModules_RespawnHelper::GetId(),
		UIModules_Countdown_Default::GetId(),
		UIModules_ScoresTable_Default::GetId(),
		UIModules_DisplayMessage_Default::GetId()
	]);
}