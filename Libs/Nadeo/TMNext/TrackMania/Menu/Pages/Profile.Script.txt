/** 
 *	Page: Profile
 */
#Const Version		"2022-08-01"
#Const ScriptName	"Libs/Nadeo/TMNext/TrackMania/Menu/Pages/Profile.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/MenuLibs/Common/Manialink/ManiaView2.Script.txt" as MV
#Include "Libs/Nadeo/MenuLibs/Common/Menu/Router_ML.Script.txt" as Router
#Include "Libs/Nadeo/TMNext/TrackMania/Menu/RoutePaths.Script.txt" as RoutePaths
#Include "Libs/Nadeo/TMNext/TrackMania/Menu/Components/ProfilePlayerInfo.Script.txt" as ProfilePlayerInfo

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Name "Page_Profile"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Get the page manialink
 *
 *	@return														The page manialink
 */
Text GetManialink() {
	return MV::Create(
C_Name, 3,
"""
<frame id="frame-global">
	<frameinstance
		modelid="{{{ProfilePlayerInfo::C_Name}}}"
		z-index="0"
		data-enable-players-pager="0"
		data-enable-zone-selection="1"
	/>
</frame>
""",
"""
""",
"""
***MainStart***
***
{{{ProfilePlayerInfo::P}}}SetBackgroundVisibility(False);
***

***MainLoop***
***
// Must be outside of the `PageIsRunning` block to receive the `LeavingRoute` event
foreach (Event in {{{Router::P}}}GetPendingEvents(This)) {
	switch (Event.Type) {
		case {{{Router::P}}}C_Event_EnteringRoute: {
			{{{ProfilePlayerInfo::P}}}SetUser(LocalUser);
			{{{ProfilePlayerInfo::P}}}Enable(True);
		}
		case {{{Router::P}}}C_Event_LeavingRoute: {
			{{{ProfilePlayerInfo::P}}}Enable(False);
		}
	}
}

if ({{{Router::P}}}PageIsRunning(This, "{{{RoutePaths::C_Path_Profile}}}")) {
	foreach (Event in {{{ProfilePlayerInfo::P}}}PendingEvents) {
		switch (Event.Type) {
			case {{{ProfilePlayerInfo::P}}}C_EventType_Close: {
				{{{Router::P}}}PushParent(This);
			}
			case {{{ProfilePlayerInfo::P}}}C_EventType_EditPlayer: {
				if ({{{Router::P}}}GetCurrentRoutePath(This) != "{{{RoutePaths::C_Path_WelcomeChangeZone}}}") {
					{{{Router::P}}}SetParentPath(This, "{{{RoutePaths::C_Path_WelcomeChangeZone}}}", {{{Router::P}}}GetCurrentRoutePath(This));
				}
				{{{Router::P}}}Push(This, "{{{RoutePaths::C_Path_WelcomeChangeZone}}}", ["{{{RoutePaths::C_Query_EditMode}}}" => "1"]);
			}
			case {{{ProfilePlayerInfo::P}}}C_EventType_Garage: {
				if ({{{Router::P}}}GetCurrentRoutePath(This) != "{{{RoutePaths::C_Path_Garage}}}") {
					{{{Router::P}}}SetParentPath(This, "{{{RoutePaths::C_Path_Garage}}}", {{{Router::P}}}GetCurrentRoutePath(This));
				}
				{{{Router::P}}}Push(This, "{{{RoutePaths::C_Path_Garage}}}");
			}
			case {{{ProfilePlayerInfo::P}}}C_EventType_MyAccessInfo: {
				if ({{{Router::P}}}GetCurrentRoutePath(This) != "{{{RoutePaths::C_Path_ClubEditionInfo}}}") {
					{{{Router::P}}}SetParentPath(This, "{{{RoutePaths::C_Path_ClubEditionInfo}}}", {{{Router::P}}}GetCurrentRoutePath(This));
				}
				{{{Router::P}}}Push(This, "{{{RoutePaths::C_Path_ClubEditionInfo}}}");
			}
		}
	}
}
***
""",
[
	Router::ComponentV2(),
	ProfilePlayerInfo::Component()
],
[]
	);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update from the ManiaApp
Void Yield() {
	
}