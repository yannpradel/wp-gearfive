/** 
 *	Get information about the build (generated by the CI)
 */
#Const Version		"2021-12-02"
#Const ScriptName	"Libs/Nadeo/CommonLibs/Common/BuildInfo.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Structures
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Struct LibCommonBuildInfo_K_BuildInfo {
	Text Commit;
	Text Date;
	Text Version;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_BuildInfoFilePath "file://Media/BuildInfo.json"
#Const C_Event_UpdateBuildInfo "Event_UpdateBuildInfo"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Globales
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
declare LibCommonBuildInfo_K_BuildInfo G_BuildInfo;

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the build info
LibCommonBuildInfo_K_BuildInfo Get() {
	return G_BuildInfo;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the build info
CHttpRequest StartLoadFile() {
	return Http.CreateGet(C_BuildInfoFilePath, False);
}
Boolean IsLoadingFile(CHttpRequest _RequestBuildInfo) {
	if (_RequestBuildInfo == Null) return False;
	
	if (_RequestBuildInfo.IsCompleted && _RequestBuildInfo.Result != "") {
		G_BuildInfo.fromjson(_RequestBuildInfo.Result);
	}
	
	return !_RequestBuildInfo.IsCompleted;
}
Void StopLoadFile(CHttpRequest _RequestBuildInfo) {
	Http.Destroy(_RequestBuildInfo);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get build info clipboard text
Text GetClipboardText(
	Text _BuildEnvironment,
	Text _APIBaseUrl,
	LibCommonBuildInfo_K_BuildInfo _BuildInfo,
	CSystemPlatform _System,
	CUser _LocalUser,
	CSystemPlatform::ESystemPlatform _Platform,
	Text _PlatformFamilyName
) {
	return """
[BuildInfo]
Environment: {{{_BuildEnvironment}}}
APIBaseUrl: {{{_APIBaseUrl}}}
Commit: {{{_BuildInfo.Commit}}}
Date: {{{_BuildInfo.Date}}}
TitleVersion: {{{_BuildInfo.Version}}}
ExeVersion: {{{_System.ExeVersion}}}
[UserInfo]
AccountId: {{{_LocalUser.WebServicesUserId}}}
Login: {{{_LocalUser.Login}}}
Name: {{{_LocalUser.Name}}}
Platform: {{{_Platform}}}
PlatformFamily: {{{_PlatformFamilyName}}}
""";
}