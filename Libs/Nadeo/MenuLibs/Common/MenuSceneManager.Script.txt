/** 
 *	Functions to help manage scenes created with `MenuSceneMgr`
 */
#Const Version		"2023-01-04"
#Const ScriptName	"Libs/Nadeo/MenuLibs/Common/MenuSceneManager.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Increment each time a scene could have been destroyed automatically by the C++
Void IncrementSceneCount(CUser _User) {
	declare Integer CommonLibs_Share_UserSceneCount for _User = 0;
	CommonLibs_Share_UserSceneCount += 1;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the number of times the scene might have been destroyed automatically by the C++
Integer GetSceneCount(CUser _User) {
	declare Integer CommonLibs_Share_UserSceneCount for _User = 0;
	return CommonLibs_Share_UserSceneCount;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Check if the page need to recreate the scene because the C++ might have destroyed it automatically
Boolean PageNeedToRecreateScene(CMlPage _Page, CUser _User) {
	declare Integer CommonLibs_Share_PageSceneCount for _Page = 0;
	declare Integer CommonLibs_Share_UserSceneCount for _User = 0;
	return (CommonLibs_Share_PageSceneCount != CommonLibs_Share_UserSceneCount);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Synchronize the scene count of the page
Void PageSynchronizeScene(CMlPage _Page, CUser _User) {
	declare Integer CommonLibs_Share_PageSceneCount for _Page = 0;
	declare Integer CommonLibs_Share_UserSceneCount for _User = 0;
	CommonLibs_Share_PageSceneCount = CommonLibs_Share_UserSceneCount;
}