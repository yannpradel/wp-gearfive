/**
 *  UI module: ToolBox client side
 */
#Const Version		"1.1.0"
#Const ScriptName	"Libs/Nadeo/TMGame/Modes/Base/UIModules/ToolBox_Client.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Libraries
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Components/ToolBox.Script.txt" as ToolBox
#Include "Libs/Nadeo/CMGame/Utils/Config.Script.txt" as Config
#Include "Libs/Nadeo/CMGame/Utils/ManiaView2.Script.txt" as MV
#Include "Libs/Nadeo/TMGame/Modes/ZIndex.Script.txt" as ZIndex

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Id "UIModule_Race_ToolBox"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Check if the toolbox is available for UIModule creation
Boolean IsAvailable() {
	return Config::HasFeature(Config::C_Feature_DebugToolBox);
}

/**
 * Get the page manialink
 * @return The page manialink
 */
Text GetML() {
	return MV::Create(C_Id, 3,
		"""
<frameinstance id="frame-toolbox" modelid="{{{ToolBox::C_Name}}}" z-index="{{{ZIndex::C_ZIndex_Toolbox}}}"/>
		""",
		"""
#Include "Libs/Nadeo/CMGame/Utils/Config.Script.txt" as Config
		""",
		"""
***MainLoop***
***
foreach (Event in PendingEvents) {
	switch (Event.Type) {
		case CMlScriptEvent::Type::KeyPress: {
			if (Config::HasFeature(Config::C_Feature_DebugToolBox)) {
				// Y (keycode 142) is pressed
				if (Input != Null && Input.IsKeyPressed(142)) {
					if (Event.KeyName == "F3") {
						{{{ToolBox::P}}}OpenClose(This);
					}
				}
			}
		}
	}
}
***
		""",
		[ToolBox::ComponentV2()],
		[]
	);
}