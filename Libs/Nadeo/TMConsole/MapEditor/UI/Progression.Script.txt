/** 
 * Progession UI
 */

#Const Version		"2022-10-12"
#Const ScriptName	"Progression.Script.txt"

#Include "TextLib" as TL
#Include "ColorLib" as CL
#Include "Libs/Nadeo/ModeLibs/Legacy/Manialink3.Script.txt" as Manialink
#Include "Libs/Nadeo/TMConsole/MapEditor/Layers.Script.txt" as Layers
#Include "Libs/Nadeo/TMNext/TrackMania/ColorPalette.Script.txt" as Colors
#Include "Libs/Nadeo/TMConsole/MapEditor/UI/Stylesheet.Script.txt" as Styles

// ---------------------------------- //
// Constants
// ---------------------------------- //
/// Steps index
#Const C_Step_Landscape		0
#Const C_Step_Start			1
#Const C_Step_Track			2
#Const C_Step_Finish		3
#Const C_Step_Deco			4
#Const C_Step_Validation	5
#Const C_Step_Save			6
#Const C_Step_Complete		7
/// Steps names
#Const C_StepsNames [
	_("Landscape"), 
	_("|BlockInfo|Start"), 
	_("Track"), 
	_("|BlockInfo|Finish"), 
	_("Decoration"), 
	_("Validation"), 
	_("Save")
]
#Const C_StepsDetails [
	_("Select a landscape. When you find one that pleases you, you can start your track."), ///< Landscape
	_("Move the start block and choose where you want to start your track."), ///< Start
	_("Build your track and place a finish block to complete it."), ///< Track
	//...
	_(""), ///< Finish
	_("Choose what kind of decoration you want and generate it. When you find one that pleases you, you can validate your track."), ///< Decoration
	_("Validate your track to be able to play it oustide the editor."), ///< Validation
	_("Don't forget to save your track!"), ///< Save
	_("Track completed.") ///< Complete
]

Text Private_GetProgressionML(Boolean _Advanced) {
	declare ColorComplete = Colors::C_Color_GreenOne;
	declare VecColorComplete = 	CL::Hex6ToRgb(ColorComplete);

	declare ColorIncomplete = Colors::C_Color_White;
	declare VecColorIncomplete = 	CL::Hex6ToRgb(ColorIncomplete);
	
	declare ColorBg = Colors::C_Color_DarkBlue;
	declare VecColorBg = 	CL::Hex6ToRgb(ColorBg);
	
	declare Steps = "";
	
	declare StepSize = 6.;
	declare InnerStepSize = StepSize * (72. / 192.);
	declare LinkSize = StepSize * (46./*60.*/ / 192.);
	
	declare BackgroundSizeX = 36.;
	declare BackgroundSizeY = 5.;
	declare DescriptionSizeY = 2.;
	declare TimelineSizeX = BackgroundSizeX - 6.;
	
	declare HiddenPos = -92. - Styles::GetPosition2(<0., 1., 0.>, Styles::CircleSize_Bottom()).Y;
	
	if (_Advanced) { // note: Random Deco isn't available in TM2020
		Steps = """
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Landscape}}}" />
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Start}}}" />
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Track}}}" />
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Finish}}}" />
<!--<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Deco}}}" />-->
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Validation}}}" />
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Save}}}" />
""";
	} else {
		Steps = """
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Start}}}" />
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Track}}}" />
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Finish}}}" />
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Validation}}}" />
<frameinstance modelid="Framemodel_Step" id="{{{C_Step_Save}}}" />
""";
	}
	
	declare Pointer = "file://Media/Manialinks/Nadeo/TMConsole/MapEditor/Images/Menu/editor-progression-arrow.dds";
	declare Circle = "file://Media/Manialinks/Nadeo/TMConsole/MapEditor/Images/Menu/disc.dds";
	
	return """
<manialink version="3" name="MapEditor:Progression">
<stylesheet>
	{{{Styles::Get()}}}
</stylesheet>
<framemodel id="Framemodel_Step">
	<label z-index="2" halign="center" valign="bottom" textcolor="{{{ColorIncomplete}}}" class="turbo2-text-progress-step" id="Label_Step" />
	<quad pos="-2 -4" z-index="0" size="30 {{{LinkSize}}}" halign="right" valign="center" bgcolor="{{{ColorIncomplete}}}" id="Quad_Link" />
	<quad pos="0 -4" z-index="1" size="{{{StepSize}}} {{{StepSize}}}" halign="center" valign="center" colorize="{{{ColorIncomplete}}}" image="{{{Circle}}}" id="Quad_Step" />
</framemodel>
<frame z-index="11" id="Frame_Global">
	<frame pos="0 {{{HiddenPos}}}" hidden="1" id="Frame_Progression">
		<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -BackgroundSizeY * 0.5, 0.>, Styles::CircleSize_Bottom()))}}}>
			{{{Styles::BuildQuad(<0., 0., 1.>, <BackgroundSizeX, BackgroundSizeY>, Styles::CircleSize_Bottom(), Styles::Align_CenterCenter2(), ["bgcolor" => ColorBg, "opacity" => "0.6", "id" => "Quad_Bg"])}}}
			{{{Styles::BuildLabel(<0., 1.5, 2.>, <BackgroundSizeX, DescriptionSizeY>, Styles::CircleSize_Bottom(), Styles::Align_CenterCenter2(), ["autonewline" => "1", "maxline" => "3", "class" => "tm2020-text-menubutton", "id" => "Label_Description"])}}}
		</frame>
		<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<-TimelineSizeX*0.5, -DescriptionSizeY-0.25, 1.>, Styles::CircleSize_Bottom()))}}}>
			<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -1., 1.>, Styles::CircleSize_Bottom()))}}} id="Frame_Steps">
				{{{Steps}}}
			</frame>
			<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<TimelineSizeX, -1., 0.>, Styles::CircleSize_Bottom()))}}} id="Frame_Play">
				<label z-index="1" halign="center" valign="bottom" textcolor="{{{ColorIncomplete}}}" text="{{{
					//L16N [MapEditor] Play on the track
					_("Play")
				}}}" class="turbo2-text-progress-step" id="Label_Step" />
				<quad pos="0 -4" z-index="0" size="30 {{{LinkSize}}}" halign="right" valign="center" bgcolor="{{{ColorIncomplete}}}" id="Quad_Link" />
				<quad pos="2 -4" z-index="0" size="{{{StepSize}}} {{{StepSize}}}" halign="center" valign="center" colorize="{{{ColorIncomplete}}}" image="{{{Pointer}}}" id="Quad_Step" />
			</frame>
		</frame>
	</frame>
</frame>
<script><!--
#Include "TextLib" as TL
{{{Manialink::DefaultIncludes()}}}

#Const C_StepsNames {{{dump(C_StepsNames)}}}
#Const C_StepsDetails {{{dump(C_StepsDetails)}}}

{{{Manialink::Animations(["EaseOutQuad"])}}}

Void SetStep(Integer _Step) {
	declare Label_Description <=> (Page.GetFirstChild("Label_Description") as CMlLabel);
	declare Frame_Steps <=> (Page.GetFirstChild("Frame_Steps") as CMlFrame);
	declare Frame_Play <=> (Page.GetFirstChild("Frame_Play") as CMlFrame);
	
	foreach (Key => Control in Frame_Steps.Controls) {
		declare Frame_Step <=> (Control as CMlFrame);
		declare Quad_Step <=> (Frame_Step.GetFirstChild("Quad_Step") as CMlQuad);
		declare Quad_Link <=> (Frame_Step.GetFirstChild("Quad_Link") as CMlQuad);
		declare Label_Step <=> (Frame_Step.GetFirstChild("Label_Step") as CMlLabel);
		
		declare Step = TL::ToInteger(Frame_Step.ControlId);
		if (Step < _Step) {
			Quad_Step.Colorize = {{{VecColorComplete}}};
			Quad_Link.BgColor = {{{VecColorComplete}}};
			Label_Step.TextColor = {{{VecColorComplete}}};
		} else {
			Quad_Step.Colorize = {{{VecColorIncomplete}}};
			Quad_Link.BgColor = {{{VecColorIncomplete}}};
			Label_Step.TextColor = {{{VecColorIncomplete}}};
		}
	}
		
	if (C_StepsDetails.existskey(_Step)) {
		Label_Description.Value = C_StepsDetails[_Step];
	} else {
		Label_Description.Value = "";
	}
	
	declare Quad_FinalStep <=> (Frame_Play.GetFirstChild("Quad_Step") as CMlQuad);
	declare Quad_FinalLink <=> (Frame_Play.GetFirstChild("Quad_Link") as CMlQuad);
	declare Label_FinalStep <=> (Frame_Play.GetFirstChild("Label_Step") as CMlLabel);
	if (_Step < {{{C_Step_Complete}}}) {
		Quad_FinalStep.Colorize = {{{VecColorIncomplete}}};
		Quad_FinalLink.BgColor = {{{VecColorIncomplete}}};
		Label_FinalStep.TextColor = {{{VecColorIncomplete}}};
	} else {
		Quad_FinalStep.Colorize = {{{VecColorComplete}}};
		Quad_FinalLink.BgColor = {{{VecColorComplete}}};
		Label_FinalStep.TextColor = {{{VecColorComplete}}};
	}
}

Void SetVisibility(Boolean _Visible) {
	if (_Visible) {
		declare L_Position for Page = <0., 0.>;
		LibManialink_Anim("<frame pos=\""^L_Position.X^" "^L_Position.Y^"\" hidden=\"0\" id=\"Frame_Progression\" />", 250, "EaseOutQuad");
	} else {
		declare L_Position for Page = <0., 0.>;
		LibManialink_Anim("<frame pos=\""^L_Position.X^" {{{HiddenPos}}}\" hidden=\"1\" id=\"Frame_Progression\" />", 250, "EaseOutQuad");
	}
}

main() {
	declare Frame_Steps <=> (Page.GetFirstChild("Frame_Steps") as CMlFrame);
	declare LinkSize = {{{Styles::GetSize2(<TimelineSizeX, 0.>, Styles::CircleSize_Bottom()).X}}} / Frame_Steps.Controls.count;
	foreach (Key => Control in Frame_Steps.Controls) {
		declare Frame_Step <=> (Control as CMlFrame);
		declare Quad_Link <=> (Frame_Step.GetFirstChild("Quad_Link") as CMlQuad);
		declare Label_Step <=> (Frame_Step.GetFirstChild("Label_Step") as CMlLabel);
		Frame_Step.ZIndex = Key*-1.;
		Frame_Step.RelativePosition_V3.X = Key * LinkSize;
		Quad_Link.Size.X = LinkSize-4.;
		if (Label_Step != Null) {
			Label_Step.Value = C_StepsNames[TL::ToInteger(Frame_Step.ControlId)];
			Label_Step.Size.X = LinkSize - 4.;
		}
		if (Key == 0) Quad_Link.Visible = False;
	}
	declare Frame_Play <=> (Page.GetFirstChild("Frame_Play") as CMlFrame);
	declare Quad_Link <=> (Frame_Play.GetFirstChild("Quad_Link") as CMlQuad);
	Quad_Link.Size.X = LinkSize-2.;
	
	
	declare Quad_Bg <=> (Page.GetFirstChild("Quad_Bg") as CMlQuad);
	
	declare L_Step for Page = -1;
	declare L_Visibility for Page = False;
	declare L_Position for Page = <0., 0.>;
	declare L_BgSizeX for Page = Quad_Bg.Size.X;
	
	declare PrevStep = -1;
	declare PrevVisibility = False;
	declare PrevPosition = <0., 0.>;
	
	while (True) {
		yield;
		
		LibManialink_AnimLoop();
		
		if (PrevStep != L_Step) {
			PrevStep = L_Step;
			SetStep(L_Step);
		}
		
		L_BgSizeX = Quad_Bg.Size.X;
		
		if (PrevVisibility != L_Visibility || PrevPosition != L_Position) {
			PrevVisibility = L_Visibility;
			PrevPosition = L_Position;
			SetVisibility(L_Visibility);
		}
	}
}
--></script>
</manialink>
""";
}

Text GetScriptVersion() { return Version; }
Text GetScriptName() { return ScriptName; }

/// Get the steps constants
Integer Step_Landscape() { return C_Step_Landscape; }
Integer Step_Start() { return C_Step_Start; }
Integer Step_Track() { return C_Step_Track; }
Integer Step_Finish() { return C_Step_Finish; }
Integer Step_Deco() { return C_Step_Deco; }
Integer Step_Validation() { return C_Step_Validation; }
Integer Step_Save() { return C_Step_Save; }
Integer Step_Complete() { return C_Step_Complete; }

// Select the current step
Void SetStep(Integer _Step) {
	declare Page <=> Layers::Get("Progression").LocalPage;
	declare L_Step for Page = -1;
	L_Step = _Step;
}

// Set the position of the UI
Void SetPosition(Vec2 _Position) {
	declare Page <=> Layers::Get("Progression").LocalPage;
	declare L_Position for Page = <0., 0.>;
	L_Position = _Position;
}

// Check if the simple menu is visible
Boolean LayerIsVisible() {
	declare Page <=> Layers::GetPage("Progression");
	declare L_Visibility for Page = False;
	return L_Visibility;
}

// Set the progession visibility
Void SetVisibility(Boolean _Visible) {
	declare Page <=> Layers::Get("Progression").LocalPage;
	declare L_Visibility for Page = False;
	L_Visibility = _Visible;
}

Real GetSizeX() {
	declare Page <=> Layers::Get("Progression").LocalPage;
	declare L_BgSizeX for Page = 0.;
	return L_BgSizeX;
}

Void Show() {
	SetVisibility(True);
}

Void Hide() {
	SetVisibility(False);
}

Void Unload() {
	Layers::Destroy("Progression");
}

Void Load(Boolean _Advanced) {
	Unload();
	
	declare LayerProgression <=> Layers::Create("Progression");
	LayerProgression.ManialinkPage = Private_GetProgressionML(_Advanced);
	
	declare Page <=> LayerProgression.LocalPage;
	declare L_Visibility for Page = False;
	declare L_Step for Page = -1;
	declare L_Position for Page = <0., 0.>;
	L_Visibility = False;
	L_Step = -1;
	L_Position = <0., -90.> + Styles::GetSize2(<0., 10.>, Styles::CircleSize_Bottom());
}