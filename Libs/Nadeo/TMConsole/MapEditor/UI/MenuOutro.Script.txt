/** 
 * Menu outro UI
 */

#Const Version		"2022-10-12"
#Const ScriptName	"MenuOutro.Script.txt"

#Include "TextLib" as TL
#Include "ColorLib" as CL
#Include "Libs/Nadeo/ModeLibs/Legacy/Manialink3.Script.txt" as Manialink
#Include "Libs/Nadeo/TMConsole/MapEditor/Layers.Script.txt" as Layers
#Include "Libs/Nadeo/TMConsole/MapEditor/Sound.Script.txt" as Sound
#Include "Libs/Nadeo/TMNext/TrackMania/ColorPalette.Script.txt" as Colors
#Include "Libs/Nadeo/TMConsole/MapEditor/UI/Stylesheet.Script.txt" as Styles

// Constants
#Const C_DefaultVisibility	False
// Selection
#Const C_Selection_Null				0
#Const C_Selection_Validate		1
//#Const C_Selection_Style			2 // TAG: MapStyleSelection
#Const C_Selection_EditTrack		3
#Const C_Selection_EditDeco		4
#Const C_Selection_Save				5
#Const C_Selection_Play				6
#Const C_Selection_Quit				7
#Const C_Selection_Test				8
#Const C_Selection_SaveAs			9
#Const C_Selection_SaveAndQuit	10

#Const C_Selection_Beginner [1, 8, /*2,*/ 3, 5, 9, 7]
#Const C_Selection_Advanced [1, 8, /*2,*/ 3, 5, 9, 7]

/// Events
#Const C_Event_Hover	"LibMenuOutro_Hover"
#Const C_Event_Click	"LibMenuOutro_Click"

// Globales
declare Boolean G_Advanced;

Text Private_GetMenuOutroML() {
	declare FullHeaderSizeX = Styles::GetHeaderSize2() + 4.;
	declare MenuSizeX = FullHeaderSizeX - 36.;
	declare MenuSizeY = 8.;
	declare MenuPosY = 8.;
	declare Shift = 0.;
	
	declare HiddenPos = -92. - Styles::GetPosition2(<0., 1., 0.>, Styles::CircleSize_Bottom()).Y;
	declare Buttons = "";
	
		Buttons ^= """	
	<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., 0.-Shift, 0.>, Styles::CircleSize_Bottom()))}}} data-value="{{{C_Selection_Validate}}}">
		{{{Styles::BuildQuad(<0., 0., 0.>, <MenuSizeX - 0.05, 1.1>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::C_Color_DarkBlue, "opacity" => "0.6", "scriptevents" => "1", "id" => "Quad_Button"])}}}
		{{{Styles::BuildLabel(<0.7, -0.55, 1.>, <MenuSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftCenter2(), [
			//L16N [MapEditor] Validate the track
			"text" => _("Validate track"), "class" => "tm2020-text-menubutton", "id" => "Label_Button", "opacity" => "0.6"
		])}}}
	</frame>
	<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -1.-Shift, 0.>, Styles::CircleSize_Bottom()))}}} data-value="{{{C_Selection_Test}}}">
		{{{Styles::BuildQuad(<0., 0., 0.>, <MenuSizeX - 0.05, 1.1>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::C_Color_DarkBlue, "opacity" => "0.6", "scriptevents" => "1", "id" => "Quad_Button"])}}}
		{{{Styles::BuildLabel(<0.7, -0.55, 1.>, <MenuSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftCenter2(), [
			//L16N [MapEditor] Edit mode where the player select a starting point to test the track
			"text" => _("Test track"), "class" => "tm2020-text-menubutton", "id" => "Label_Button", "opacity" => "0.6"
		])}}}
	</frame>
	""";
	
		/*// TAG: MapStyleSelection
			Shift += 1.;
			Buttons ^= """
	<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -1.-Shift, 0.>, Styles::CircleSize_Bottom()))}}} data-value="{{{C_Selection_Style}}}">
		{{{Styles::BuildQuad(<0., 0., 0.>, <MenuSizeX - 0.05, 1.1>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::C_Color_DarkBlue, "opacity" => "0.6", "scriptevents" => "1", "id" => "Quad_Button"])}}}
		{{{Styles::BuildLabel(<0.7, -0.55, 1.>, <MenuSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftCenter2(), [
			//L16N [MapEditor] Select a style for the track (tech, lol, fullspeed, ...)
			"text" => _("Select style"), "class" => "tm2020-text-menubutton", "id" => "Label_Button", "opacity" => "0.6"
		])}}}
	</frame>
	""";
	*/
		
		Buttons ^= """	
	<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -2.-Shift, 0.>, Styles::CircleSize_Bottom()))}}} data-value="{{{C_Selection_EditTrack}}}">
		{{{Styles::BuildQuad(<0., 0., 0.>, <MenuSizeX - 0.05, 1.1>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::C_Color_DarkBlue, "opacity" => "0.6", "scriptevents" => "1", "id" => "Quad_Button"])}}}
		{{{Styles::BuildLabel(<0.7, -0.55, 1.>, <MenuSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftCenter2(), [
			//L16N [MapEditor] Edit the track
			"text" => _("Edit track"), "class" => "tm2020-text-menubutton", "id" => "Label_Button", "opacity" => "0.6"
		])}}}
	</frame>
	""";
		
		/*if (G_Advanced) { // Random deco is not available in TM2020
			Shift += 1;
			Buttons ^= """
			<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -2.-Shift, 0.>, Styles::CircleSize_Bottom()))}}} data-value="{{{C_Selection_EditDeco}}}">
				{{{Styles::BuildQuad(<0., 0., 0.>, <MenuSizeX - 0.05, 1.1>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::C_Color_DarkBlue, "opacity" => "0.6", "scriptevents" => "1", "id" => "Quad_Button"])}}}
				{{{Styles::BuildLabel(<0.7, -0.55, 1.>, <MenuSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftCenter2(), [
					//L16N [MapEditor] Edit the decoration
					"text" => _("Edit decoration"), "class" => "tm2020-text-menubutton", "id" => "Label_Button", "opacity" => "0.6"
				])}}}
			</frame>
			""";
		}*/
		
		Buttons ^= """
	<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -3.-Shift, 0.>, Styles::CircleSize_Bottom()))}}} data-value="{{{C_Selection_Save}}}">
		{{{Styles::BuildQuad(<0., 0., 0.>, <MenuSizeX - 0.05, 1.1>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::C_Color_DarkBlue, "opacity" => "0.6", "scriptevents" => "1", "id" => "Quad_Button"])}}}
		{{{Styles::BuildLabel(<0.7, -0.55, 1.>, <MenuSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftCenter2(), [
			//L16N [MapEditor] Save the track
			"text" => _("Save"), "class" => "tm2020-text-menubutton", "id" => "Label_Button", "opacity" => "0.6"
		])}}}
	</frame>
	""";
	
		
		Shift += 1.;
			Buttons ^= """
		<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -3.-Shift, 0.>, Styles::CircleSize_Bottom()))}}} data-value="{{{C_Selection_SaveAs}}}">
			{{{Styles::BuildQuad(<0., 0., 0.>, <MenuSizeX - 0.05, 1.1>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::C_Color_DarkBlue, "opacity" => "0.6", "scriptevents" => "1", "id" => "Quad_Button"])}}}
			{{{Styles::BuildLabel(<0.7, -0.55, 1.>, <MenuSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftCenter2(), [
				//L16N [MapEditor] Save the track in a new file
				"text" => _("Save as new"), "class" => "tm2020-text-menubutton", "id" => "Label_Button", "opacity" => "0.6"
			])}}}
		</frame>
		""";
		
		Buttons ^= """
	<!--<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -4.-Shift, 0.>, Styles::CircleSize_Bottom()))}}} data-value="{{{C_Selection_Play}}}">
		{{{Styles::BuildQuad(<0., 0., 0.>, <MenuSizeX - 0.05, 1.1>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::C_Color_DarkBlue, "opacity" => "0.6", "scriptevents" => "1", "id" => "Quad_Button"])}}}
		{{{Styles::BuildLabel(<0.7, -0.55, 1.>, <MenuSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftCenter2(), [
			//L16N [MapEditor] Play on the track
			"text" => _("Play"), "class" => "tm2020-text-menubutton", "id" => "Label_Button", "opacity" => "0.6"
		])}}}
	</frame>-->
	<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -4.-Shift, 0.>, Styles::CircleSize_Bottom()))}}} data-value="{{{C_Selection_Quit}}}">
		{{{Styles::BuildQuad(<0., 0., 0.>, <MenuSizeX - 0.05, 1.1>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::C_Color_DarkBlue, "opacity" => "0.6", "scriptevents" => "1", "id" => "Quad_Button"])}}}
		{{{Styles::BuildLabel(<0.7, -0.55, 1.>, <MenuSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftCenter2(), [
			//L16N [MapEditor] Leave the track editor
			"text" => _("Quit"), "class" => "tm2020-text-menubutton", "id" => "Label_Button", "opacity" => "0.6"
		])}}}
	</frame>
	""";
	
	return """
<manialink version="3" name="MapEditor:MenuOutro">
<stylesheet>
	{{{Styles::Get()}}}
</stylesheet>
<frame id="Frame_Global">
	<frame pos="0 {{{HiddenPos}}}" hidden="1" id="Frame_MenuOutro">
		<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<-FullHeaderSizeX*0.5, 0., 0.>, Styles::CircleSize_Bottom()))}}}>
			<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., 0., 1.>, Styles::CircleSize_Bottom()))}}} id="Frame_Buttons">
				{{{Buttons}}}
			</frame>
		</frame>
	</frame>
</frame>
<script><!--
#Include "TextLib" as TL
#Include "AnimLib" as AL
#Include "ColorLib" as CL
{{{Manialink::DefaultIncludes()}}}

declare Integer[] G_Selectables;
declare CMlQuad G_QuadSelected;
declare CMlLabel G_LabelSelected;

{{{Manialink::Animations(["EaseOutQuad"])}}}

Void InitSelectables() {
	G_Selectables.clear();
	declare Frame_Buttons <=> (Page.GetFirstChild("Frame_Buttons") as CMlFrame);
	foreach (Control in Frame_Buttons.Controls) {
		declare Value for Control = -1;
		Value = TL::ToInteger(Control.DataAttributeGet("value"));
		G_Selectables.add(Value);
		
		declare Frame_Button <=> (Control as CMlFrame);
		declare Quad_Button <=> (Frame_Button.GetFirstChild("Quad_Button") as CMlQuad);
		declare ItemKey for Quad_Button = -1;
		ItemKey = Value;
	}
}

Void SelectButton(Integer _Selected) {
	declare Frame_Buttons <=> (Page.GetFirstChild("Frame_Buttons") as CMlFrame);
	foreach (Control in Frame_Buttons.Controls) {
		declare Frame_Button <=> (Control as CMlFrame);
		declare Quad_Button <=> (Frame_Button.GetFirstChild("Quad_Button") as CMlQuad);
		declare Label_Button <=> (Frame_Button.GetFirstChild("Label_Button") as CMlLabel);
		declare Value for Frame_Button = -1;
		declare WasSelected for Frame_Button = False;
		if (Value == _Selected && !WasSelected) {
			G_QuadSelected <=> Quad_Button;
			G_LabelSelected <=> Label_Button;
			WasSelected = True;
			
			LibManialink_Anim(Quad_Button, {{{Manialink::Inject("""<quad opacity="1"/>""")}}}, 25, "EaseOutQuad");
			LibManialink_Anim(Label_Button, {{{Manialink::Inject("""<label opacity="1" />""")}}}, 25, "EaseOutQuad");
		} else if (Value != _Selected && WasSelected) {
			WasSelected = False;
			LibManialink_Anim(Quad_Button, {{{Manialink::Inject("""<quad opacity=".6"/>""")}}}, 25, "EaseOutQuad");
			LibManialink_Anim(Label_Button, {{{Manialink::Inject("""<label opacity=".6" />""")}}}, 25, "EaseOutQuad");
		}
	}
}

Void SetVisibility(Boolean _Visible) {
	if (_Visible) {
		LibManialink_Anim({{{Manialink::Inject("""<frame pos="0 {{{Styles::Margin_Bottom()+Styles::GetSize2(<0., MenuPosY>, Styles::CircleSize_Bottom()).Y+0.7}}}" hidden="0" id="Frame_MenuOutro" />""")}}}, 250, "EaseOutQuad");
	} else {
		LibManialink_Anim({{{Manialink::Inject("""<frame pos="0 {{{HiddenPos}}}" hidden="1" id="Frame_MenuOutro" />""")}}}, 250, "EaseOutQuad");
	}
}

Void SetValidationStatus(CEditorPlugin::ValidationStatus _ValidationStatus) {
	/*declare Button_Play <=> (Page.GetFirstChild("Button_Play") as CMlFrame);
	declare Quad_Button <=> (Button_Play.GetFirstChild("Quad_Button") as CMlQuad);
	declare Label_Button <=> (Button_Play.GetFirstChild("Label_Button") as CMlLabel);
	
	if (_ValidationStatus == CEditorPlugin::ValidationStatus::Validated) {
		Label_Button.TextColor = CL::Hex6ToRgb("{{{Colors::C_Color_White}}}");
	} else {
		Label_Button.TextColor = CL::Hex6ToRgb("{{{Colors::C_Color_LightGrey}}}");
	}*/
}

main() {
	declare L_Visibility for Page = {{{C_DefaultVisibility}}};
	declare L_SelectedUpdate for Page = -1;
	declare L_Selected for Page = -1;
	
	declare PrevValidationStatus = CEditorPlugin::ValidationStatus::NotValidable;
	declare PrevVisibility = {{{C_DefaultVisibility}}};
	declare PrevSelectedUpdate = -1;
	
	G_Selectables.clear();
	
	InitSelectables();
	SetValidationStatus(PrevValidationStatus);
	
	declare AnimDir = 1;
	declare AnimStartTime = Now;
	declare AnimDuration = 350;
	
	while (True) {
		yield;
		
		LibManialink_AnimLoop();
		
		if (PrevValidationStatus != Editor.ValidationStatus) {
			PrevValidationStatus = Editor.ValidationStatus;
			SetValidationStatus(Editor.ValidationStatus);
		}
		
		if (PrevVisibility != L_Visibility) {
			PrevVisibility = L_Visibility;
			SetVisibility(L_Visibility);
		}
		
		if (PrevSelectedUpdate != L_SelectedUpdate) {
			PrevSelectedUpdate = L_SelectedUpdate;
			SelectButton(L_Selected);
			
			AnimDir = 1;
			AnimStartTime = Now;
		}
		
		foreach (Event in PendingEvents) {
			if (Event.Type == CMlScriptEvent::Type::MouseOver) {
				if (Event.Control != Null && Event.ControlId == "Quad_Button") {
					declare ItemKey for Event.Control = -1;
					SendCustomEvent("{{{C_Event_Hover}}}", [TL::ToText(ItemKey)]);
				}
			} else if (Event.Type == CMlScriptEvent::Type::MouseClick) {
				if (Event.Control != Null && Event.ControlId == "Quad_Button") {
					declare ItemKey for Event.Control = -1;
					SendCustomEvent("{{{C_Event_Click}}}", [TL::ToText(ItemKey)]);
				}
			}
		}
		
		if (L_Visibility && G_QuadSelected != Null && G_LabelSelected != Null) {
			G_QuadSelected.Opacity = 1.;
		}
	}
}
--></script>
</manialink>	
""";
}

Text GetScriptVersion() { return Version; }
Text GetScriptName() { return ScriptName; }

/// Get the events constants
Text Event_Hover() { return C_Event_Hover; }
Text Event_Click() { return C_Event_Click; }

// ---------------------------------- //
/// Get the selection constants
Integer Selection_Validate() { return C_Selection_Validate; }
Integer Selection_Test() { return C_Selection_Test; }
//Integer Selection_Style() { return C_Selection_Style; } // TAG: MapStyleSelection
Integer Selection_Play() { return C_Selection_Play; }
Integer Selection_Save() { return C_Selection_Save; }
Integer Selection_SaveAs() { return C_Selection_SaveAs; }
Integer Selection_EditTrack() { return C_Selection_EditTrack; }
Integer Selection_EditDeco() { return C_Selection_EditDeco; }
Integer Selection_Quit() { return C_Selection_Quit; }
Integer Selection_SaveAndQuit() { return C_Selection_SaveAndQuit; }


//Get the available menu selection
Integer[] GetMenuSelection() {	
	if (G_Advanced) return C_Selection_Advanced;
	
	return C_Selection_Beginner;
}

// Select a button in the menu outro
Void Select(Integer _Selection, Boolean _PlaySound) {
	if (!GetMenuSelection().exists(_Selection)) return;
	
	declare Page <=> Layers::GetPage("MenuOutro");
	declare L_SelectedUpdate for Page = -1;
	declare L_Selected for Page = C_Selection_Null;
	L_Selected = _Selection;
	L_SelectedUpdate = Now;
}

/// Overload Select()
Void Select(Integer _Selection) {
	Select(_Selection, False);
}

// Select the next or the previous button in the menu outro
Void Shift(Integer _Shift) {
	declare Page <=> Layers::GetPage("MenuOutro");
	declare L_SelectedUpdate for Page = -1;
	declare L_Selected for Page = C_Selection_Null;
	declare Selection = GetMenuSelection();
	declare Key = Selection.keyof(L_Selected);
	if (Key < 0) Key = 0;
	
	Key += _Shift;
	if (Key < 0) Key = Selection.count - 1;
	else if (Key > Selection.count - 1) Key = 0;
	
	L_Selected = Selection[Key];
	L_SelectedUpdate = Now;
}

Void Next() {
	Sound::Play("EditorMenuMove");
	Shift(1);
}

Void Prev() {
	Sound::Play("EditorMenuMove");
	Shift(-1);
}

// Check if the menu outro is visible
Boolean LayerIsVisible() {
	declare Page <=> Layers::GetPage("MenuOutro");
	declare L_Visibility for Page = C_DefaultVisibility;
	return L_Visibility;
}

//  Set the menu outro visibility
Void SetVisibility(Boolean _Visible) {
	declare Page <=> Layers::GetPage("MenuOutro");
	declare L_Visibility for Page = C_DefaultVisibility;
	
	if (_Visible && !L_Visibility) {
		Select(C_Selection_Validate);
	}
	
	L_Visibility = _Visible;
}

Void Show() {
	SetVisibility(True);
}

Void Hide() {
	SetVisibility(False);
}

// Valid the current selection
Integer Valid() {
	declare Page <=> Layers::GetPage("MenuOutro");
	declare L_Selected for Page = C_Selection_Null;
	
	Sound::Play("EditorMenuValid");
	if (GetMenuSelection().exists(L_Selected)) return L_Selected;
	return C_Selection_Null;
}

Void Unload() {
	Layers::Destroy("MenuOutro");
}

Void Load(Boolean _Advanced) {
	Unload();
	
	G_Advanced = _Advanced;
	
	declare LayerMenuOutro <=> Layers::Create("MenuOutro");
	LayerMenuOutro.ManialinkPage = Private_GetMenuOutroML();
	
	declare Page <=> LayerMenuOutro.LocalPage;
	declare L_Visibility for Page = C_DefaultVisibility;
	declare L_Selected for Page = C_Selection_Null;
	declare L_SelectedUpdate for Page = -1;
	L_Visibility = C_DefaultVisibility;
	L_Selected = C_Selection_Null;
	L_SelectedUpdate = -1;
	
	Hide();
}