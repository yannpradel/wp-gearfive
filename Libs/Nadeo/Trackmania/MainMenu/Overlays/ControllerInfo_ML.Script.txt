/**
 *	Overlay: ControllerInfo
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/MainMenu/Overlays/ControllerInfo_ML.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
Void Private_Update() {
	declare Integer Trackmania_ControllerInfo_ButtonsUpdate for System = 0;
	Trackmania_ControllerInfo_ButtonsUpdate += 1;
}

Void SetButtons(Text[Text] _Buttons) {
	declare Text[Text] Trackmania_ControllerInfo_Buttons for System = [];
	declare Boolean IsUpdateNeeded = _Buttons.count != Trackmania_ControllerInfo_Buttons.count;

	if (!IsUpdateNeeded) {
		foreach (Action => Description in _Buttons) {
			if (!Trackmania_ControllerInfo_Buttons.existskey(Action) || Trackmania_ControllerInfo_Buttons[Action] != Description) {
				IsUpdateNeeded = True;
				break;
			}
		}
	}

	if (IsUpdateNeeded) {
		Trackmania_ControllerInfo_Buttons = _Buttons;
		Private_Update();
	}
}

Void SetButton(Text _Action, Text _Description) {
	declare Text[Text] Trackmania_ControllerInfo_Buttons for System = [];
	if (!Trackmania_ControllerInfo_Buttons.existskey(_Action) || Trackmania_ControllerInfo_Buttons[_Action] != _Description) {
		Trackmania_ControllerInfo_Buttons[_Action] = _Description;
		Private_Update();
	}
}

Text[Text] GetButtons() {
	declare Text[Text] Trackmania_ControllerInfo_Buttons for System = [];
	return Trackmania_ControllerInfo_Buttons;
}