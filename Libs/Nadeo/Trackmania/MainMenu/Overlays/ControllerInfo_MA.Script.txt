/**
 *	Overlay: ControllerInfo
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/MainMenu/Overlays/ControllerInfo_MA.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Utils/ManiaView2.Script.txt" as MV
#Include "Libs/Nadeo/CMGame/Utils/Stylesheet.Script.txt" as Stylesheet
#Include "Libs/Nadeo/CMGame/Components/ButtonsList2.Script.txt" as ButtonsList
#Include "Libs/Nadeo/CMGame/Menus/Router_ML.Script.txt" as Router
#Include "Libs/Nadeo/Trackmania/MainMenu/Constants.Script.txt" as Const

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Name "Overlay_ControllerInfo"
#Const C_ResetOnRouteChange True

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Get the overlay manialink
 *
 *	@return														The overlay manialink
 */
Text GetManialink() {
	return MV::Create(
C_Name, 3,
"""
<frame id="frame-global" z-index="{{{Const::C_ZIndex_ControllerInfo}}}">
	<frame pos="160 -92" id="frame-buttons">
		<frameinstance modelid="{{{ButtonsList::C_Name}}}"
			id="buttons-list-controller-info"
			data-halign="right"
			data-valign="bottom"
			data-buttonmaxwidth="50."
			data-textprefix="$t"
			data-textfont="{{{Stylesheet::GetFont(Stylesheet::C_Font_Weight_SemiBold)}}}"
			data-buttons-spacing="5."
		/>
	</frame>
</frame>
""",
"""
#Include "Libs/Nadeo/CMGame/Utils/Tools.Script.txt" as Tools
""",
"""
***MainInit***
***
declare CMlFrame Frame_Buttons;
declare CMlFrame ButtonsList_ControllerInfo;

declare Text[Text] Trackmania_ControllerInfo_Buttons for System = [];
declare Integer Trackmania_ControllerInfo_ButtonsUpdate for System = 0;
declare Text Trackmania_ControllerInfo_CurrentRoutePath for System = "";
declare Boolean Trackmania_ControllerInfo_HideForMouse for System = False;

declare Integer LocalButtonsUpdate;
declare Boolean LocalHideForMouse;
declare Boolean UsingMouse;

Trackmania_ControllerInfo_Buttons = [];
Trackmania_ControllerInfo_ButtonsUpdate = 0;
Trackmania_ControllerInfo_CurrentRoutePath = "";
***

***MainStart***
***
Frame_Buttons <=> (Page.GetFirstChild("frame-buttons") as CMlFrame);
ButtonsList_ControllerInfo <=> (Frame_Buttons.GetFirstChild("buttons-list-controller-info") as CMlFrame);

UsingMouse = Tools::IsUsingMouse(Input);
Frame_Buttons.Visible = !LocalHideForMouse || !UsingMouse;
***

***MainLoop***
***
if (PageIsVisible) {
	if (LocalButtonsUpdate != Trackmania_ControllerInfo_ButtonsUpdate) {
		LocalButtonsUpdate = Trackmania_ControllerInfo_ButtonsUpdate;
		{{{ButtonsList::P}}}SetButtons(ButtonsList_ControllerInfo, Trackmania_ControllerInfo_Buttons);
	}

	if (LocalHideForMouse != Trackmania_ControllerInfo_HideForMouse) {
		LocalHideForMouse = Trackmania_ControllerInfo_HideForMouse;
		Frame_Buttons.Visible = !LocalHideForMouse || !UsingMouse;
	}

	if (LocalHideForMouse) {
		if (UsingMouse != Tools::IsUsingMouse(Input)) {
			UsingMouse = Tools::IsUsingMouse(Input);
			Frame_Buttons.Visible = !UsingMouse;
		}
	}

	if (Trackmania_ControllerInfo_CurrentRoutePath != {{{Router::P}}}GetCurrentRoutePath(This)) {
		Trackmania_ControllerInfo_CurrentRoutePath = {{{Router::P}}}GetCurrentRoutePath(This);
	}
}
***
""",
[
	ButtonsList::Component(),
	Router::ComponentV2()
],
[]
	);
}

Void HideForMouse(Boolean _Hide) {
	declare Boolean  Trackmania_ControllerInfo_HideForMouse for System = False;
	Trackmania_ControllerInfo_HideForMouse = _Hide;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update from the ManiaApp
Void Yield() {
	if (C_ResetOnRouteChange) {
		foreach (Event in PendingEvents) {
			if (
				Event.Type == CManiaAppEvent::EType::LayerCustomEvent &&
				Event.CustomEventType == Router::C_Event_RouteUpdated &&
				Event.CustomEventData.count >= 2
			) {
				declare Text Trackmania_ControllerInfo_CurrentRoutePath for System = "";
				if (Trackmania_ControllerInfo_CurrentRoutePath != Event.CustomEventData[0]) {
					declare Text[Text] Trackmania_ControllerInfo_Buttons for System = [];
					declare Integer Trackmania_ControllerInfo_ButtonsUpdate for System = 0;
					Trackmania_ControllerInfo_Buttons = [];
					Trackmania_ControllerInfo_ButtonsUpdate += 1;
					Trackmania_ControllerInfo_CurrentRoutePath = Event.CustomEventData[0];
				}
			}
		}
	}
}