/**
 *	Page: Settings
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/MainMenu/Pages/Settings.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Utils/ManiaView2.Script.txt" as MV
#Include "Libs/Nadeo/Trackmania/MainMenu/RoutePaths.Script.txt" as RoutePaths
#Include "Libs/Nadeo/CMGame/Menus/Router_ML.Script.txt" as Router
#Include "Libs/Nadeo/Trackmania/Components/Settings.Script.txt" as Settings

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Name "Page_Settings"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the page manialink
Text GetManialink() {
	return MV::Create(
C_Name, 3,
"""
<frame id="frame-global">
	<frameinstance modelid="{{{Settings::C_Name}}}" />
</frame>
""",
"""
""",
"""
***MainInit***
***
declare Boolean ResetSettingCategory = True;
***

***MainStart***
***
{{{Settings::P}}}EnableCredits(True);
{{{Settings::P}}}EnableProfile(True);
{{{Settings::P}}}EnableLatencyTest(True);
{{{Settings::P}}}EnableControllerInfo(True);
{{{Settings::P}}}SetBackgroundVisibility(False);
***

***MainLoop***
***
foreach (Event in {{{Router::P}}}GetPendingEvents(This)) {
	switch (Event.Type) {
		case "{{{Router::C_Event_LeavingRoute}}}": {
			{{{Settings::P}}}Enable(False);
		}
	}
}

if ({{{Router::P}}}PageIsRunning(This, "{{{RoutePaths::C_Path_Settings}}}")) {
	foreach (Event in {{{Router::P}}}GetPendingEvents(This)) {
		switch (Event.Type) {
			case "{{{Router::C_Event_EnteringRoute}}}": {
				{{{Settings::P}}}Enable(True, ResetSettingCategory);
				ResetSettingCategory = True;
			}
		}
	}

	foreach (Event in {{{Settings::P}}}PendingEvents) {
		switch (Event.Type) {
			case {{{Settings::P}}}C_EventType_Back: {
				{{{Router::P}}}PushParent(This);
			}
			case {{{Settings::P}}}C_EventType_OpenCredits: {
				ResetSettingCategory = False;
				{{{Router::P}}}SetParentPath(This, "{{{RoutePaths::C_Path_Credits}}}", "{{{RoutePaths::C_Path_Settings}}}");
				{{{Router::P}}}Push(This, "{{{RoutePaths::C_Path_Credits}}}");
			}
			case {{{Settings::P}}}C_EventType_OpenProfile: {
				ResetSettingCategory = False;
				{{{Router::P}}}SetParentPath(This, "{{{RoutePaths::C_Path_Profile}}}", "{{{RoutePaths::C_Path_Settings}}}");
				{{{Router::P}}}Push(This, "{{{RoutePaths::C_Path_Profile}}}");
			}
		}
	}
}
***
""",
[
	Router::ComponentV2(),
	Settings::Component()
],
[]
	);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update from the ManiaApp
Void Yield() {

}