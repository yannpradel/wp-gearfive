/**
 *	`DirectLink` helpers functions
 */
#Const Version		"2.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/MainMenu/DirectLink.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Structures
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Struct K_MatchmakingStatus {
	Boolean IsInQueue;
	Text QueueId;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_MatchmakingStatus_Null K_MatchmakingStatus {
	IsInQueue = False,
	QueueId = ""
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update the matchmaking status of the user
Void SetMatchmakingStatus(CUser _User, Boolean _IsInQueue, Text _QueueId) {
	declare K_MatchmakingStatus Trackmania_DirectLink_MatchmakingStatus for _User = C_MatchmakingStatus_Null;
	Trackmania_DirectLink_MatchmakingStatus = K_MatchmakingStatus {
		IsInQueue = _IsInQueue,
		QueueId = _QueueId
	};
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the matchmaking status of the user
K_MatchmakingStatus GetMatchmakingStatus(CUser _User) {
	declare K_MatchmakingStatus Trackmania_DirectLink_MatchmakingStatus for _User = C_MatchmakingStatus_Null;
	return Trackmania_DirectLink_MatchmakingStatus;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Allow `DirectLink` to cancel a queue in the matchmaking page
Void RequestMatchmakingPageQueueCancel(CUser _User) {
	declare Integer Trackmania_DirectLink_MatchmakingQueueCancel for _User = 0;
	Trackmania_DirectLink_MatchmakingQueueCancel += 1;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Check if `DirectLink` requested to cancel a queue in the matchmaking page
Integer GetMatchmakingPageQueueCancel(CUser _User) {
	declare Integer Trackmania_DirectLink_MatchmakingQueueCancel for _User = 0;
	return Trackmania_DirectLink_MatchmakingQueueCancel;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Check if `DirectLink` is processing
Boolean IsProcessing(CUser _User) {
	declare Boolean Trackmania_DirectLink_IsProcessing for _User = False;
	return Trackmania_DirectLink_IsProcessing;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Set the processing state of the `DirectLink`
Void SetProcessingState(CUser _User, Boolean _IsProcessing) {
	declare Boolean Trackmania_DirectLink_IsProcessing for _User = False;
	Trackmania_DirectLink_IsProcessing = _IsProcessing;
}

/* @Changelog

Version 2.0.0
-------------

- Only use text ids (instead of numeric) for matchmaking
 */