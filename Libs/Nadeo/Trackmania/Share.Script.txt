/**
 *	Share lib to share information between the menu and the map editor plugins
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/Share.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Libraries
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/Trackmania/Structures/CampaignStruct.Script.txt" as CampaignStruct

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Structure
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Struct K_MapInfo {
	Text Name;
	Text Type;
	Text Path;
	Text FileName;
	Text Uid;
	Text AuthorLogin;
	Integer AuthorTime;
	Integer GoldTime;
	Integer SilverTime;
	Integer BronzeTime;
	Integer NbLaps;
	Boolean IsPlayable;
	Text CollectionName;
	Boolean GoToServerReview;
	Boolean GoToHotSeat;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_MapInfo_Null K_MapInfo {
	Name = "",
	Type = "",
	Path = "",
	FileName = "",
	Uid = "",
	AuthorLogin = "",
	AuthorTime = -1,
	GoldTime = -1,
	SilverTime = -1,
	BronzeTime = -1,
	NbLaps = 1,
	IsPlayable = False,
	CollectionName = "",
	GoToServerReview = False,
	GoToHotSeat = False
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
Void SetLastMapCreated(Text _MapName, Text _MapFileName, CMapInfo _MapInfo, Boolean _GoToServerReview, Boolean _GoToHotSeat) {
	declare K_MapInfo LibShare_EditedMapInfo for System = C_MapInfo_Null;

	LibShare_EditedMapInfo.Name = _MapName;
	LibShare_EditedMapInfo.FileName = _MapFileName;
	LibShare_EditedMapInfo.GoToServerReview = _GoToServerReview;
	LibShare_EditedMapInfo.GoToHotSeat = _GoToHotSeat;

	if (_MapInfo == Null) {
		LibShare_EditedMapInfo.Uid = C_MapInfo_Null.Uid;
		LibShare_EditedMapInfo.Path = C_MapInfo_Null.Path;
		LibShare_EditedMapInfo.Type = C_MapInfo_Null.Type;
		LibShare_EditedMapInfo.AuthorLogin = C_MapInfo_Null.AuthorLogin;
		LibShare_EditedMapInfo.AuthorTime = C_MapInfo_Null.AuthorTime;
		LibShare_EditedMapInfo.GoldTime = C_MapInfo_Null.GoldTime;
		LibShare_EditedMapInfo.SilverTime = C_MapInfo_Null.SilverTime;
		LibShare_EditedMapInfo.BronzeTime = C_MapInfo_Null.BronzeTime;
		LibShare_EditedMapInfo.NbLaps = C_MapInfo_Null.NbLaps;
		LibShare_EditedMapInfo.IsPlayable = C_MapInfo_Null.IsPlayable;
		LibShare_EditedMapInfo.CollectionName = C_MapInfo_Null.CollectionName;
	} else {
		LibShare_EditedMapInfo.Uid = _MapInfo.MapUid;
		LibShare_EditedMapInfo.Path = _MapInfo.Path;
		LibShare_EditedMapInfo.Type = _MapInfo.MapType;
		LibShare_EditedMapInfo.AuthorLogin = _MapInfo.AuthorLogin;
		LibShare_EditedMapInfo.AuthorTime = _MapInfo.TMObjective_AuthorTime;
		LibShare_EditedMapInfo.GoldTime = _MapInfo.TMObjective_GoldTime;
		LibShare_EditedMapInfo.SilverTime = _MapInfo.TMObjective_SilverTime;
		LibShare_EditedMapInfo.BronzeTime = _MapInfo.TMObjective_BronzeTime;
		LibShare_EditedMapInfo.NbLaps = _MapInfo.TMObjective_NbLaps;
		LibShare_EditedMapInfo.IsPlayable = _MapInfo.IsPlayable;
		LibShare_EditedMapInfo.CollectionName = _MapInfo.CollectionName;
	}
}
Void SetLastMapCreated(Text _MapName, Text _MapPath, CMapInfo _MapInfo) {
	SetLastMapCreated(_MapName, _MapPath, _MapInfo, False, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
Void ResetLastMapCreated() {
	SetLastMapCreated("", "", Null);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
K_MapInfo GetLastMapCreated() {
	declare K_MapInfo LibShare_EditedMapInfo for System = C_MapInfo_Null;
	return LibShare_EditedMapInfo;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
Void SetGoToServerReview(Boolean _GoToServerReview) {
	declare K_MapInfo LibShare_EditedMapInfo for System = C_MapInfo_Null;
	LibShare_EditedMapInfo.GoToServerReview = _GoToServerReview;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
Boolean GetGoToServerReview() {
	declare K_MapInfo LibShare_EditedMapInfo for System = C_MapInfo_Null;
	return LibShare_EditedMapInfo.GoToServerReview;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
Void SetGoToHotSeat(Boolean _GoToHotSeat) {
	declare K_MapInfo LibShare_EditedMapInfo for System = C_MapInfo_Null;
	LibShare_EditedMapInfo.GoToHotSeat = _GoToHotSeat;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
Boolean GetGoToHotSeat() {
	declare K_MapInfo LibShare_EditedMapInfo for System = C_MapInfo_Null;
	return LibShare_EditedMapInfo.GoToHotSeat;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
CampaignStruct::LibCampaignStruct_K_Map ConvertToMap(K_MapInfo _MapInfo) {
	return CampaignStruct::LibCampaignStruct_K_Map {
		Uid = _MapInfo.Uid,
		Name = _MapInfo.Name,
		Type = _MapInfo.Type,
		LocalFileName = _MapInfo.FileName,
		AuthorLogin = _MapInfo.AuthorLogin,
		AuthorTime = _MapInfo.AuthorTime,
		GoldTime = _MapInfo.GoldTime,
		SilverTime = _MapInfo.SilverTime,
		BronzeTime = _MapInfo.BronzeTime,
		NbLaps = _MapInfo.NbLaps,
		IsPlayable = _MapInfo.IsPlayable,
		CollectionName = _MapInfo.CollectionName,
		ThumbnailUrl = "file://Thumbnails/MapUid/"^_MapInfo.Uid
	};
}