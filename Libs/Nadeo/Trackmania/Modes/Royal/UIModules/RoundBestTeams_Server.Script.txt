/**
 *	UI module: RoundBestTeams server side
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/Modes/Royal/UIModules/RoundBestTeams_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Modes/UIModules_Common.Script.txt" as UIModules_Common
#Include "Libs/Nadeo/Trackmania/Modes/Royal/UIModules/RoundBestTeams_Common.Script.txt" as UIModules_RoundBestTeams_Common

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the id of the UI module
Text GetId() {
	return UIModules_RoundBestTeams_Common::C_Id;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the configuration of the UI module
UIModules_Common::K_ModuleConfig GetConfig() {
	return UIModules_RoundBestTeams_Common::C_UIModuleConfig;
}
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the number of slots displayed for the best teams
Integer GetSlotsNb() {
	return UIModules_RoundBestTeams_Common::C_SlotsNb;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the duration of the elimination animation
Integer GetAnimationDuration(Integer _BestTeamsNb) {
	return (
		UIModules_RoundBestTeams_Common::C_AnimDuration_UIShow +
		(UIModules_RoundBestTeams_Common::C_AnimDuration_BestTeam + UIModules_RoundBestTeams_Common::C_AnimDuration_BestTeamInverval) * _BestTeamsNb +
		UIModules_RoundBestTeams_Common::C_AnimDuration_AfterBestTeams +
		UIModules_RoundBestTeams_Common::C_AnimDuration_UIHide +
		250 //< Add a bit of time to compensate ping variation
	);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Send number of best teams to display at the end of the round
Integer SendBestTeamsNb(Integer _BestTeamsNb) {
	declare netwrite Integer Net_Royal_RoundEliminations_BestTeamsNb for Teams[0];
	Net_Royal_RoundEliminations_BestTeamsNb = _BestTeamsNb;

	return GetAnimationDuration(_BestTeamsNb);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;
	declare CUIConfig UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;

	declare netwrite Boolean Net_Royal_RoundBestTeams_IsVisible for UI;
	Net_Royal_RoundBestTeams_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;

	SetVisibility(_Player, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}