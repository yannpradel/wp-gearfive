/**
 *	UI module: SegmentComplete server side
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/Modes/Royal/UIModules/SegmentComplete_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Modes/UIModules_Common.Script.txt" as UIModules_Common
#Include "Libs/Nadeo/Trackmania/Modes/Royal/UIModules/SegmentComplete_Common.Script.txt" as UIModules_SegmentComplete_Common

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the id of the UI module
Text GetId() {
	return UIModules_SegmentComplete_Common::C_Id;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the configuration of the UI module
UIModules_Common::K_ModuleConfig GetConfig() {
	return UIModules_SegmentComplete_Common::C_UIModuleConfig;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// A player completed a segment for their team
Void CompleteSegment(
	Integer _TeamId,
	Integer _Segment,
	Integer _LoopNb,
	Text _AccountId,
	Boolean _CanJoin,
	Boolean _OnlyBonusTime,
	Integer _BonusTime
) {
	declare netwrite UIModules_SegmentComplete_Common::K_LastSegmentActivation[Integer] Net_Royal_SegmentComplete_LastSegmentActivations for Teams[0];
	declare netwrite Integer Net_Royal_SegmentComplete_LastSegmentActivationsUpdate for Teams[0];
	Net_Royal_SegmentComplete_LastSegmentActivations[_TeamId] = UIModules_SegmentComplete_Common::K_LastSegmentActivation {
		Time = Now,
		Segment = _Segment,
		AccountId = _AccountId,
		CanJoin = _CanJoin,
		OnlyBonusTime = _OnlyBonusTime,
		BonusTime = _BonusTime,
		LoopNb = _LoopNb
	};
	Net_Royal_SegmentComplete_LastSegmentActivationsUpdate += 1;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Reset all segment activations
Void ResetSegments() {
	declare netwrite UIModules_SegmentComplete_Common::K_LastSegmentActivation[Integer] Net_Royal_SegmentComplete_LastSegmentActivations for Teams[0];
	declare netwrite Integer Net_Royal_SegmentComplete_LastSegmentActivationsUpdate for Teams[0];
	Net_Royal_SegmentComplete_LastSegmentActivations = [];
	Net_Royal_SegmentComplete_LastSegmentActivationsUpdate += 1;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;

	declare netwrite Boolean Net_Royal_SegmentComplete_IsVisible for _Player;
	Net_Royal_SegmentComplete_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;

	SetVisibility(_Player, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	ResetSegments();

	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}