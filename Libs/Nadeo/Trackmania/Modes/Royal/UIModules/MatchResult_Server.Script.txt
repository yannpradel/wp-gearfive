/**
 *	UI module: MatchResult server side
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/Modes/Royal/UIModules/MatchResult_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Modes/UIModules_Common.Script.txt" as UIModules_Common
#Include "Libs/Nadeo/Trackmania/Modes/Royal/UIModules/MatchResult_Common.Script.txt" as UIModules_MatchResult_Common

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the id of the UI module
Text GetId() {
	return UIModules_MatchResult_Common::C_Id;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the configuration of the UI module
UIModules_Common::K_ModuleConfig GetConfig() {
	return UIModules_MatchResult_Common::C_UIModuleConfig;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update the teams rank after their elimination
Void SetTeamsRank(Integer[Integer] _TeamsRank) {
	declare netwrite Integer[Integer] Net_Royal_MatchResult_TeamsRank for Teams[0];
	declare netwrite Integer Net_Royal_MatchResult_TeamsRankUpdate for Teams[0];
	Net_Royal_MatchResult_TeamsRank = _TeamsRank;
	Net_Royal_MatchResult_TeamsRankUpdate += 1;
}
Void ResetTeamsRank() {
	SetTeamsRank([]);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update a team rank after its elimination
Void SetTeamRank(Integer _TeamId, Integer _TeamRank) {
	declare netwrite Integer[Integer] Net_Royal_MatchResult_TeamsRank for Teams[0];
	declare netwrite Integer Net_Royal_MatchResult_TeamsRankUpdate for Teams[0];
	Net_Royal_MatchResult_TeamsRank[_TeamId] = _TeamRank;
	Net_Royal_MatchResult_TeamsRankUpdate += 1;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Reset the fact that the player clicked on the stay button
Void ResetClickedOnStay() {
	declare netwrite Integer Net_Royal_MatchResult_ResetClickedOnStay for Teams[0];
	Net_Royal_MatchResult_ResetClickedOnStay += 1;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Tell if this is the last round of the match
Void SetLastRound(Boolean _IsLastRound) {
	declare netwrite Boolean Net_Royal_MatchResult_IsLastRound for Teams[0];
	Net_Royal_MatchResult_IsLastRound = _IsLastRound;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;
	declare CUIConfig UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;

	declare netwrite Boolean Net_Royal_MatchResult_IsVisible for UI;
	Net_Royal_MatchResult_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;

	SetVisibility(_Player, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	ResetTeamsRank();
	ResetClickedOnStay();
	SetLastRound(False);

	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}