/**
 *	UI module: TeamScore server side
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/Modes/Royal/UIModules/TeamScore_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Modes/UIModules_Common.Script.txt" as UIModules_Common
#Include "Libs/Nadeo/Trackmania/Modes/Royal/UIModules/TeamScore_Common.Script.txt" as UIModules_TeamScore_Common

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the id of the UI module
Text GetId() {
	return UIModules_TeamScore_Common::C_Id;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the configuration of the UI module
UIModules_Common::K_ModuleConfig GetConfig() {
	return UIModules_TeamScore_Common::C_UIModuleConfig;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Set the rank where the eliminations start
Void SetEliminationRank(Integer _EliminationRank) {
	declare netwrite Integer Net_Royal_TeamScore_EliminationRank for Teams[0];
	Net_Royal_TeamScore_EliminationRank = _EliminationRank;
}
Void ResetEliminationRank() {
	SetEliminationRank(-1);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Set the number of teams not eliminated
Void SetNotEliminatedTeamsNb(Integer _NotEliminatedTeamsNb) {
	declare netwrite Integer Net_Royal_TeamScore_NotEliminatedTeamsNb for Teams[0];
	Net_Royal_TeamScore_NotEliminatedTeamsNb = _NotEliminatedTeamsNb;
}
Void ResetNotEliminatedTeamsNb() {
	SetNotEliminatedTeamsNb(-1);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update the teams bonus time
Void SetTeamBonusTime(Integer _TeamId, Integer _BonusTime) {
	declare netwrite Integer[Integer] Net_Royal_TeamScore_TeamsBonusTime for Teams[0];
	declare netwrite Integer Net_Royal_TeamScore_TeamsBonusTimeUpdate for Teams[0];
	Net_Royal_TeamScore_TeamsBonusTime[_TeamId] = _BonusTime;
	Net_Royal_TeamScore_TeamsBonusTimeUpdate += 1;
}
Void ResetTeamsBonusTime() {
	declare netwrite Integer[Integer] Net_Royal_TeamScore_TeamsBonusTime for Teams[0];
	declare netwrite Integer Net_Royal_TeamScore_TeamsBonusTimeUpdate for Teams[0];
	Net_Royal_TeamScore_TeamsBonusTime = [];
	Net_Royal_TeamScore_TeamsBonusTimeUpdate += 1;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;
	declare CUIConfig UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;

	declare netwrite Boolean Net_Royal_TeamScore_IsVisible for UI;
	Net_Royal_TeamScore_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;

	SetVisibility(_Player, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	ResetTeamsBonusTime();

	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	ResetEliminationRank();
	ResetNotEliminatedTeamsNb();
	Unload();
}