/**
 *	State manager for the TM_Teams_Matchmaking_Online mode
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/Modes/Teams_Matchmaking/StateManager.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Libraries
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Modes/UIModules_Server.Script.txt" as UIModules
#Include "Libs/Nadeo/TMGame/Modes/AFK_Server.Script.txt" as UIModules_AFK
#Include "Libs/Nadeo/Trackmania/Modes/TimeAttack/UIModules/BestRaceViewer_Server.Script.txt" as UIModules_BestRaceViewer
#Include "Libs/Nadeo/Trackmania/Modes/Teams_Matchmaking/UIModules/StartMatch_Teams_Server.Script.txt" as UIModules_StartMatch_Teams
#Include "Libs/Nadeo/Trackmania/Modes/Teams_Matchmaking/UIModules/EndMatch_Teams_Server.Script.txt" as UIModules_EndMatch_Teams
#Include "Libs/Nadeo/Trackmania/Modes/Teams_Matchmaking/UIModules/Ranking_Matchmaking_Server.Script.txt" as UIModules_Ranking_Matchmaking
#Include "Libs/Nadeo/Trackmania/Modes/Teams_Matchmaking/UIModules/WarmUp_Matchmaking_Server.Script.txt" as UIModules_WarmUp_Matchmaking
#Include "Libs/Nadeo/Trackmania/Modes/Teams_Matchmaking/UIModules/ScoreAndMapInfos_Teams_Server.Script.txt" as UIModules_ScoreAndMapInfos_Teams
#Include "Libs/Nadeo/Trackmania/Modes/Teams_Matchmaking/UIModules/LiveRanking_Server.Script.txt" as UIModules_LiveRanking
#Include "Libs/Nadeo/Trackmania/Modes/Teams_Matchmaking/UIModules/NetShare_Teams_Server.Script.txt" as NetShare_Teams

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/*	Enabling this setting will have
 *	a big impact on the script performances
 *	in crowded server. The library
 *	will do a check on every player
 *	of the server on every frame.
 *	If the script respect the convention
 *	to always use MB_Yield() instead of yield;
 *	then it should not have to enforce
 *	the state at each frame. Any new
 *	player will be initialized when
 *	they join the server
 */
#Const C_ForceStatesEveryFrame False

#Const C_State_Waiting 0
#Const C_State_Playing 1
#Const C_State_WarmUp 2
#Const C_State_StartMatch 3
#Const C_State_Ranking 4
#Const C_State_EndMatch 5

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Globales
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
declare Integer[] G_ForcedStates;

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Apply the given states to the given player
 *
 *	@param	_Player										The player to update
 *	@param	_States										The states to apply
 */
Void Private_Apply(CPlayer _Player, Integer[] _States) {
	if (_Player == Null) return;

	UIModules_BestRaceViewer::SetVisibility(_Player, False);
	UIModules_StartMatch_Teams::SetVisibility(_Player, False);
	UIModules_EndMatch_Teams::SetVisibility(_Player, False);
	UIModules_Ranking_Matchmaking::SetVisibility(_Player, False);
	UIModules_WarmUp_Matchmaking::SetVisibility(_Player, False);
	UIModules_LiveRanking::SetVisibility(_Player, False);
	UIModules_ScoreAndMapInfos_Teams::SetVisibility(_Player, False);

	foreach (State in _States) {
		switch (State) {
			case C_State_Waiting: {
				UIModules_LiveRanking::SetVisibility(_Player, True);
				UIModules_ScoreAndMapInfos_Teams::SetVisibility(_Player, True);
			}
			case C_State_WarmUp: {
				UIModules_WarmUp_Matchmaking::SetVisibility(_Player, True);
				UIModules_LiveRanking::SetVisibility(_Player, True);
			}
			case C_State_StartMatch: {
				UIModules_StartMatch_Teams::SetVisibility(_Player, True);
			}
			case C_State_Playing: {
				UIModules_BestRaceViewer::SetVisibility(_Player, True);
				UIModules_LiveRanking::SetVisibility(_Player, True);
				UIModules_ScoreAndMapInfos_Teams::SetVisibility(_Player, True);
			}
			case C_State_Ranking: {
				UIModules_Ranking_Matchmaking::SetVisibility(_Player, True);
			}
			case C_State_EndMatch: {
				UIModules_EndMatch_Teams::SetVisibility(_Player, True);
			}
		}
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Add a state to a player
 *
 *	@param	_Player										The player to update
 *	@param	_State										The state to add
 */
Void Add(CPlayer _Player, Integer _State) {
	if (_Player == Null) return;
	declare Integer[] LibStateMgr_States for _Player;
	if (!LibStateMgr_States.exists(_State)) {
		LibStateMgr_States.add(_State);
		if (G_ForcedStates.count <= 0) {
			Private_Apply(_Player, LibStateMgr_States);
		}
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Remove a state from a player
 *
 *	@param	_Player										The player to update
 *	@param	_State										The state to remove
 */
Void Remove(CPlayer _Player, Integer _State) {
	if (_Player == Null) return;
	declare Integer[] LibStateMgr_States for _Player;
	if (LibStateMgr_States.exists(_State)) {
		declare Removed = LibStateMgr_States.remove(_State);
		if (G_ForcedStates.count <= 0) {
			Private_Apply(_Player, LibStateMgr_States);
		}
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Remove all state from a player
 *
 *	@param	_Player										The player to reset
 */
Void Reset(CPlayer _Player) {
	if (_Player == Null) return;
	declare Integer[] LibStateMgr_States for _Player;
	LibStateMgr_States = [];
	if (G_ForcedStates.count <= 0) {
		Private_Apply(_Player, LibStateMgr_States);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Check if the player has the given state
 *
 *	@param	_Player										The player to check
 *	@param	_State										The state to find
 *
 *	@return														True if the player has the state
 *																		False otherwise
 */
Boolean HasState(CPlayer _Player, Integer _State) {
	if (_Player == Null) return False;

	declare Integer[] LibStateMgr_States for _Player;
	return LibStateMgr_States.exists(_State) || G_ForcedStates.exists(_State);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Check if the players have the given state as forced
 *
 *	@param	_State										The state to find
 *
 *	@return														True if the players has the state
 *																		False otherwise
 */
Boolean HasForcedState(Integer _State) {
	return G_ForcedStates.exists(_State);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Force the states applied to all players
 *	To disable the forced states use an empty array
 *
 *	@param	_States									The states to force
 */
Void ForcePlayersStates(Integer[] _States) {
	G_ForcedStates = _States;

	foreach (Player in AllPlayers) {
		declare Boolean LibStateMgr_IsForcedStates for Player = False;
		// Enable
		if (G_ForcedStates.count > 0) {
			LibStateMgr_IsForcedStates = True;
			Private_Apply(Player, G_ForcedStates);
		}
		// Disable
		else {
			LibStateMgr_IsForcedStates = False;
			declare Integer[] LibStateMgr_States for Player;
			Private_Apply(Player, LibStateMgr_States);
		}
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update the library
Void Yield() {
	if (C_ForceStatesEveryFrame && G_ForcedStates.count > 0) {
		foreach (Player in AllPlayers) {
			declare Boolean LibStateMgr_IsForcedStates for Player = False;
			if (!LibStateMgr_IsForcedStates) {
				LibStateMgr_IsForcedStates = True;
				Private_Apply(Player, G_ForcedStates);
			}
		}
	}

	UIModules_AFK::Yield();
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;

	UIModules_BestRaceViewer::InitializePlayer(_Player);
	UIModules_StartMatch_Teams::InitializePlayer(_Player);
	UIModules_EndMatch_Teams::InitializePlayer(_Player);
	UIModules_Ranking_Matchmaking::InitializePlayer(_Player);
	UIModules_WarmUp_Matchmaking::InitializePlayer(_Player);
	UIModules_ScoreAndMapInfos_Teams::InitializePlayer(_Player);
	UIModules_LiveRanking::InitializePlayer(_Player);
	UIModules_AFK::InitializePlayer(_Player);

	Reset(_Player);

	if (G_ForcedStates.count > 0) {
		declare Boolean LibStateMgr_IsForcedStates for _Player = False;
		LibStateMgr_IsForcedStates = True;
		Private_Apply(_Player, G_ForcedStates);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	ForcePlayersStates([]);

	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}

	UIModules_ScoreAndMapInfos_Teams::Unload();
	UIModules_LiveRanking::Unload();
	UIModules_WarmUp_Matchmaking::Unload();
	UIModules_Ranking_Matchmaking::Unload();
	UIModules_EndMatch_Teams::Unload();
	UIModules_StartMatch_Teams::Unload();
	UIModules_BestRaceViewer::Unload();
	UIModules_AFK::Unload();
	NetShare_Teams::Unload();
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();

	UIModules_BestRaceViewer::Load();
	UIModules_StartMatch_Teams::Load();
	UIModules_EndMatch_Teams::Load();
	UIModules_Ranking_Matchmaking::Load();
	UIModules_WarmUp_Matchmaking::Load();
	UIModules_ScoreAndMapInfos_Teams::Load();
	UIModules_LiveRanking::Load();
	UIModules_AFK::Load();
	NetShare_Teams::Load();

	UIModules::InitializeCustomizableModule(UIModules_BestRaceViewer::GetConfig());
	UIModules::InitializeCustomizableModule(UIModules_StartMatch_Teams::GetConfig());
	UIModules::InitializeCustomizableModule(UIModules_EndMatch_Teams::GetConfig());
	UIModules::InitializeCustomizableModule(UIModules_Ranking_Matchmaking::GetConfig());
	UIModules::InitializeCustomizableModule(UIModules_WarmUp_Matchmaking::GetConfig());
	UIModules::InitializeCustomizableModule(UIModules_ScoreAndMapInfos_Teams::GetConfig());
	UIModules::InitializeCustomizableModule(UIModules_LiveRanking::GetConfig());
	UIModules::InitializeCustomizableModule(UIModules_AFK::GetConfig());

	UIModules::LoadModules([
		UIModules_BestRaceViewer::GetId(),
		UIModules_StartMatch_Teams::GetId(),
		UIModules_EndMatch_Teams::GetId(),
		UIModules_Ranking_Matchmaking::GetId(),
		UIModules_WarmUp_Matchmaking::GetId(),
		UIModules_ScoreAndMapInfos_Teams::GetId(),
		UIModules_LiveRanking::GetId(),
		UIModules_AFK::GetId()
	]);
}