/**
 *	UI module: Header server side
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/Modes/TMWTCommon/UIModules/Header_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Modes/UIModules_Common.Script.txt" as UIModules_Common
#Include "Libs/Nadeo/Trackmania/Modes/TMWTCommon/UIModules/Header_Common.Script.txt" as UIModules_Header_Common

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_DefaultVisibility False

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the id of the UI module
Text GetId() {
	return UIModules_Header_Common::C_Id;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the configuration of the UI module
UIModules_Common::K_ModuleConfig GetConfig() {
	return UIModules_Header_Common::C_UIModuleConfig;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Send the points to the clients
Void SetMatchPointsLimit(Integer _MatchPointsLimit) {
	declare netwrite Integer Net_TMWTCommon_Header_MatchPointsLimit for Teams[0] = 0;
	Net_TMWTCommon_Header_MatchPointsLimit = _MatchPointsLimit;
}
Void SetMapPointsLimit(Integer _MapPointsLimit) {
	declare netwrite Integer Net_TMWTCommon_Header_MapPointsLimit for Teams[0] = 0;
	Net_TMWTCommon_Header_MapPointsLimit = _MapPointsLimit;
}
Void SetClansMatchPoints(Integer _Clan1MatchPoints, Integer _Clan2MatchPoints) {
	declare netwrite Int2 Net_TMWTCommon_Header_ClansMatchPoints for Teams[0] = <0, 0>;
	Net_TMWTCommon_Header_ClansMatchPoints = <_Clan1MatchPoints, _Clan2MatchPoints>;
}
Void SetClansMapPoints(Integer _Clan1MapPoints, Integer _Clan2MapPoints) {
	declare netwrite Int2 Net_TMWTCommon_Header_ClansMapPoints for Teams[0] = <0, 0>;
	Net_TMWTCommon_Header_ClansMapPoints = <_Clan1MapPoints, _Clan2MapPoints>;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;
	declare CUIConfig UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;

	declare netwrite Boolean Net_TMWTCommon_Header_IsVisible for UI = C_DefaultVisibility;
	Net_TMWTCommon_Header_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;

	SetVisibility(_Player, C_DefaultVisibility);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	SetMatchPointsLimit(0);
	SetMapPointsLimit(0);
	SetClansMatchPoints(0, 0);
	SetClansMapPoints(0, 0);

	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}