/**
 *	UI module: TMWTMarkers server side
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/Modes/TMWTCommon/UIModules/TMWTMarkers_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Modes/UIModules_Common.Script.txt" as UIModules_Common
#Include "Libs/Nadeo/Trackmania/Modes/TMWTCommon/UIModules/TMWTMarkers_Common.Script.txt" as UIModules_TMWTMarkers_Common

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_DefaultVisibility False

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the id of the UI module
Text GetId() {
	return UIModules_TMWTMarkers_Common::C_Id;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the configuration of the UI module
UIModules_Common::K_ModuleConfig GetConfig() {
	return UIModules_TMWTMarkers_Common::C_UIModuleConfig;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Send the frame id of the visible markers
Void SetMarkerFrameId(Integer _MarkerFrameId, Text _AccountId) {
	declare netwrite Text[Integer] Net_TMWTCommon_TMWTMarkers_MarkerFrameIds for Teams[0] = [];
	declare netwrite Integer Net_TMWTCommon_TMWTMarkers_MarkerFrameIdsUpdate for Teams[0] = 0;
	Net_TMWTCommon_TMWTMarkers_MarkerFrameIds[_MarkerFrameId] = _AccountId;
	Net_TMWTCommon_TMWTMarkers_MarkerFrameIdsUpdate += 1;
}
Void RemoveMarkerFrameId(Integer _MarkerFrameId) {
	declare netwrite Text[Integer] Net_TMWTCommon_TMWTMarkers_MarkerFrameIds for Teams[0] = [];
	declare netwrite Integer Net_TMWTCommon_TMWTMarkers_MarkerFrameIdsUpdate for Teams[0] = 0;
	Net_TMWTCommon_TMWTMarkers_MarkerFrameIds.removekey(_MarkerFrameId);
	Net_TMWTCommon_TMWTMarkers_MarkerFrameIdsUpdate += 1;
}
Void ResetMarkerFrameIds() {
	declare netwrite Text[Integer] Net_TMWTCommon_TMWTMarkers_MarkerFrameIds for Teams[0] = [];
	declare netwrite Integer Net_TMWTCommon_TMWTMarkers_MarkerFrameIdsUpdate for Teams[0] = 0;
	Net_TMWTCommon_TMWTMarkers_MarkerFrameIds = [];
	Net_TMWTCommon_TMWTMarkers_MarkerFrameIdsUpdate += 1;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;
	declare CUIConfig UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;

	declare netwrite Boolean Net_TMWTCommon_TMWTMarkers_IsVisible for UI = C_DefaultVisibility;
	Net_TMWTCommon_TMWTMarkers_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;

	SetVisibility(_Player, C_DefaultVisibility);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	ResetMarkerFrameIds();

	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}