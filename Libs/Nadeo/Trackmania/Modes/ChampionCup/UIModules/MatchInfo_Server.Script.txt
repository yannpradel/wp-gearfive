/**
 *	UI module: MatchInfo server side
 */
#Const Version		"1.0.0"
#Const ScriptName	"Libs/Nadeo/Trackmania/Modes/ChampionCup/UIModules/MatchInfo_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Modes/UIModules_Common.Script.txt" as UIModules_Common
#Include "Libs/Nadeo/Trackmania/Modes/ChampionCup/UIModules/MatchInfo_Common.Script.txt" as UIModules_MatchInfo_Common

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the id of the UI module
Text GetId() {
	return UIModules_MatchInfo_Common::C_Id;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the configuration of the UI module
UIModules_Common::K_ModuleConfig GetConfig() {
	return UIModules_MatchInfo_Common::C_UIModuleConfig;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Send the best lap time
Void SetBestLap(Text _WebServicesUserId, Integer _Time, Boolean _IsWorldRecord, Boolean _IsInit) {
	declare netwrite Integer Net_ChampionCup_MatchInfo_BestLapUpdate for Teams[0];
	declare netwrite Text Net_ChampionCup_MatchInfo_BestLapPlayer for Teams[0];
	declare netwrite Integer Net_ChampionCup_MatchInfo_BestLapTime for Teams[0];
	declare netwrite Boolean Net_ChampionCup_MatchInfo_IsWorldRecord for Teams[0];
	declare netwrite Boolean Net_ChampionCup_MatchInfo_IsInit for Teams[0];
	Net_ChampionCup_MatchInfo_BestLapUpdate += 1;
	Net_ChampionCup_MatchInfo_BestLapPlayer = _WebServicesUserId;
	Net_ChampionCup_MatchInfo_BestLapTime = _Time;
	Net_ChampionCup_MatchInfo_IsWorldRecord = _IsWorldRecord;
	Net_ChampionCup_MatchInfo_IsInit = _IsInit;
}
Void SetBestLap(Text _WebServicesUserId, Integer _Time) {
	SetBestLap(_WebServicesUserId, _Time, False, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Show or hide the UI
 *
 *	@param	_Player										The player to update
 *	@param	_Display									True to show
 *																		False to hide
 */
Void SetVisibility(CPlayer _Player, Boolean _Display) {
	if (_Player == Null) return;
	declare CUIConfig UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;

	declare netwrite Boolean Net_ChampionCup_MatchInfo_IsVisible for UI;
	Net_ChampionCup_MatchInfo_IsVisible = _Display;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize a player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CPlayer _Player) {
	if (_Player == Null) return;

	SetVisibility(_Player, False);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	SetBestLap("", 0);

	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}