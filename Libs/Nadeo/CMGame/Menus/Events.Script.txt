/**
 *	Events relayer for the ManiaApp
 */
#Const Version		"1.0.1"
#Const ScriptName	"Libs/Nadeo/CMGame/Menus/Events.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Includes
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "Libs/Nadeo/CMGame/Utils/Layers.Script.txt" as Layers
#Include "Libs/Nadeo/CMGame/Utils/ManiaView2.Script.txt" as MV

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Name "Events_EventRelayer"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Send an event from the ManiaApp
 *	that will be relayed back to the
 *	ManiaApp through a manialink.
 *	This allow to create an event
 *	communication bus between between
 *	different parts of the ManiaApp
 *
 *	@param	_Type											A unique identifier for the event
 *	@param	_Data											The data to transmit with the event
 */
Void Send(Text _Type, Text[] _Data) {
	declare CUILayer Layer <=> Layers::Get(C_Name);
	if (Layer != Null) {
		LayerCustomEvent(Layer, _Type, _Data);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Get the manialink that will relay
 *	the events sent by the mania app
 *	back to the mania app itself
 */
Text GetEventRelayML() {
	return MV::Create(
C_Name, 3,
"""
""",
"""
""",
"""
***MainLoop***
***
foreach (Event in PendingEvents) {
	if (Event.Type == CMlScriptEvent::Type::PluginCustomEvent) {
		declare Text[] Data;
		foreach (Value in Event.CustomEventData) {
			Data.add(Value);
		}
		SendCustomEvent(Event.CustomEventType, Data);
	}
}
***
""",
[],
[]
	);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	Layers::Destroy(C_Name);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();

	Layers::Create(C_Name, GetEventRelayML());
}