/**
 *	Check on which platform the game is running
 */
#Const Version		"2.0.0"
#Const ScriptName	"Libs/Nadeo/CMGame/Utils/Platform.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Family_Unknown 0
#Const C_Family_Console 1
#Const C_Family_Streaming 2
#Const C_Family_PC 3
#Const C_FamilyNames [
	0 => "Unknown",
	1 => "Console",
	2 => "Streaming",
	3 => "PC"
]
// Duplicate this constant in the component after each change
// We cannot dump the constant because it converts
// `CSystemPlatform::ESystemPlatform` to `::ESystemPlatform`
#Const C_PlatformToFamily [
	CSystemPlatform::ESystemPlatform::PS4 => 1,
	CSystemPlatform::ESystemPlatform::PS5 => 1,
	CSystemPlatform::ESystemPlatform::XBoxOne => 1,
	CSystemPlatform::ESystemPlatform::XBoxSeries => 1,
	CSystemPlatform::ESystemPlatform::Stadia => 2,
	CSystemPlatform::ESystemPlatform::Luna => 2,
	CSystemPlatform::ESystemPlatform::None => 3,
	CSystemPlatform::ESystemPlatform::Steam => 3,
	CSystemPlatform::ESystemPlatform::UPlay => 3
]

#Const C_FirstParty_Unknown 0
#Const C_FirstParty_Sony 1
#Const C_FirstParty_Microsoft 2
#Const C_FirstParty_Google 3
#Const C_FirstParty_Amazon 4
// Duplicate this constant in the component after each change
// We cannot dump the constant because it converts
// `CSystemPlatform::ESystemPlatform` to `::ESystemPlatform`
#Const C_PlatformToFirstParty [
	CSystemPlatform::ESystemPlatform::PS4 => 1,
	CSystemPlatform::ESystemPlatform::PS5 => 1,
	CSystemPlatform::ESystemPlatform::XBoxOne => 2,
	CSystemPlatform::ESystemPlatform::XBoxSeries => 2,
	CSystemPlatform::ESystemPlatform::Stadia => 3,
	CSystemPlatform::ESystemPlatform::Luna => 4,
	CSystemPlatform::ESystemPlatform::None => 0,
	CSystemPlatform::ESystemPlatform::Steam => 0,
	CSystemPlatform::ESystemPlatform::UPlay => 0
]
#Const C_FirstPartiesWithForcedButtonIcon [1, 2]

// The C++ restricts the first-party logo color to pure white (#ffffff) or pure black (#000000)
// Any other color is converted to pure white
#Const C_LogoWhite_Sony "$<$z$FFF$>"
#Const C_LogoBlack_Sony "$<$z$000$>"
#Const C_LogoWhite_Amazon "$<$z$FFF$>"
#Const C_LogoBlack_Amazon "$<$z$000$>"
#Const C_LogoWhite_Google "$<$z$FFF$>"
#Const C_LogoBlack_Google "$<$z$000$>"
#Const C_LogoWhite_Microsoft "$<$z$FFF$>"
#Const C_LogoBlack_Microsoft "$<$z$000$>"
#Const C_LogoWhite_Ubisoft "$<$z$FFF$>"
#Const C_LogoBlack_Ubisoft "$<$z$000$>"

#Const C_NetworkName_Ubisoft "$<$zUbisoft Connect$>"
#Const C_NetworkName_Sony "$<$z PlayStation™Network $>"
#Const C_NetworkName_Microsoft "$<$zXbox network$>"
#Const C_NetworkName_Google "$<$zStadia network$>"
#Const C_NetworkName_Amazon "$<$zLuna network$>"

#Const C_SmallNetworkName_Ubisoft "$<$zUbisoft Connect$>"
#Const C_SmallNetworkName_Sony "$<$zPSN$>"
#Const C_SmallNetworkName_Microsoft "$<$zXbox$>"
#Const C_SmallNetworkName_Google "$<$zStadia$>"
#Const C_SmallNetworkName_Amazon "$<$zLuna$>"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the current platform
CSystemPlatform::ESystemPlatform GetPlatform(CSystemPlatform _System) {
	if (_System == Null) return CSystemPlatform::ESystemPlatform::None;

	declare CSystemPlatform::ESystemPlatform CMGame_Platform_OverridePlatform for _System = CSystemPlatform::ESystemPlatform::None;
	if (CMGame_Platform_OverridePlatform != CSystemPlatform::ESystemPlatform::None) {
		return CMGame_Platform_OverridePlatform;
	}

	return _System.Platform;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Override the current plaform
Void OverridePlatform(CSystemPlatform _System, CSystemPlatform::ESystemPlatform _Platform) {
	declare CSystemPlatform::ESystemPlatform CMGame_Platform_OverridePlatform for _System = CSystemPlatform::ESystemPlatform::None;
	CMGame_Platform_OverridePlatform = _Platform;
}
CSystemPlatform::ESystemPlatform GetPlatformOverride(CSystemPlatform _System) {
	declare CSystemPlatform::ESystemPlatform CMGame_Platform_OverridePlatform for _System = CSystemPlatform::ESystemPlatform::None;
	return CMGame_Platform_OverridePlatform;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the family of platform the game is running on
Integer GetFamily(CSystemPlatform::ESystemPlatform _Platform) {
	return C_PlatformToFamily.get(_Platform, C_Family_Unknown);
}
Integer GetFamily(CSystemPlatform _System) {
	return GetFamily(GetPlatform(_System));
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the name of the platform family
Text GetFamilyName(Integer _Family) {
	return C_FamilyNames.get(_Family, C_FamilyNames[C_Family_Unknown]);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Check if the game is running on a specific platform family
Boolean IsPC(CSystemPlatform::ESystemPlatform _Platform) {
	return GetFamily(_Platform) == C_Family_PC;
}
Boolean IsPC(CSystemPlatform _System) {
	return IsPC(GetPlatform(_System));
}
Boolean IsConsole(CSystemPlatform::ESystemPlatform _Platform) {
	return GetFamily(_Platform) == C_Family_Console;
}
Boolean IsConsole(CSystemPlatform _System) {
	return IsConsole(GetPlatform(_System));
}
Boolean IsStreaming(CSystemPlatform::ESystemPlatform _Platform) {
	return GetFamily(_Platform) == C_Family_Streaming;
}
Boolean IsStreaming(CSystemPlatform _System) {
	return IsStreaming(GetPlatform(_System));
}
Boolean IsFromFirstParty(CSystemPlatform::ESystemPlatform _Platform) {
	declare Integer Family = GetFamily(_Platform);
	return (
		Family == C_Family_Console ||
		Family == C_Family_Streaming
	);
}
Boolean IsFromFirstParty(CSystemPlatform _System) {
	return IsFromFirstParty(GetPlatform(_System));
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Get the First Party associated to the platform the game is running on
Integer GetFirstParty(CSystemPlatform::ESystemPlatform _Platform) {
	return C_PlatformToFirstParty.get(_Platform, C_FirstParty_Unknown);
}
Integer GetFirstParty(CSystemPlatform _System) {
	return GetFirstParty(GetPlatform(_System));
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Check if the game is running on a specific First Party platform
Boolean IsSony(CSystemPlatform::ESystemPlatform _Platform) {
	return GetFirstParty(_Platform) == C_FirstParty_Sony;
}
Boolean IsSony(CSystemPlatform _System) {
	return IsSony(GetPlatform(_System));
}
Boolean IsMicrosoft(CSystemPlatform::ESystemPlatform _Platform) {
	return GetFirstParty(_Platform) == C_FirstParty_Microsoft;
}
Boolean IsMicrosoft(CSystemPlatform _System) {
	return IsMicrosoft(GetPlatform(_System));
}
Boolean IsGoogle(CSystemPlatform::ESystemPlatform _Platform) {
	return GetFirstParty(_Platform) == C_FirstParty_Google;
}
Boolean IsGoogle(CSystemPlatform _System) {
	return IsGoogle(GetPlatform(_System));
}
Boolean IsAmazon(CSystemPlatform::ESystemPlatform _Platform) {
	return GetFirstParty(_Platform) == C_FirstParty_Amazon;
}
Boolean IsAmazon(CSystemPlatform _System) {
	return IsAmazon(GetPlatform(_System));
}
Boolean IsUnknownFirstParty(CSystemPlatform::ESystemPlatform _Platform) {
	return GetFirstParty(_Platform) == C_FirstParty_Unknown;
}
Boolean IsUnknownFirstParty(CSystemPlatform _System) {
	return IsUnknownFirstParty(GetPlatform(_System));
}

Text GetNetworkName(CSystemPlatform::ESystemPlatform _Platform) {
	switch (GetFirstParty(_Platform)) {
		case C_FirstParty_Sony: return C_NetworkName_Sony;
		case C_FirstParty_Amazon: return C_NetworkName_Amazon;
		case C_FirstParty_Google: return C_NetworkName_Google;
		case C_FirstParty_Microsoft: return C_NetworkName_Microsoft;
	}
	return C_NetworkName_Ubisoft;
}
Text GetNetworkName(CSystemPlatform _System) {
	return GetNetworkName(GetPlatform(_System));
}

Text GetSmallNetworkName(CSystemPlatform::ESystemPlatform _Platform) {
	switch (GetFirstParty(_Platform)) {
		case C_FirstParty_Sony: return C_SmallNetworkName_Sony;
		case C_FirstParty_Amazon: return C_SmallNetworkName_Amazon;
		case C_FirstParty_Google: return C_SmallNetworkName_Google;
		case C_FirstParty_Microsoft: return C_SmallNetworkName_Microsoft;
	}
	return C_SmallNetworkName_Ubisoft;
}
Text GetSmallNetworkName(CSystemPlatform _System) {
	return GetSmallNetworkName(GetPlatform(_System));
}

Text GetFirstPartyLogo(CSystemPlatform::ESystemPlatform _Platform) {
	switch (GetFirstParty(_Platform)) {
		case C_FirstParty_Sony: return C_LogoWhite_Sony;
		case C_FirstParty_Amazon: return C_LogoWhite_Amazon;
		case C_FirstParty_Google: return C_LogoWhite_Google;
		case C_FirstParty_Microsoft: return C_LogoWhite_Microsoft;
	}
	return C_LogoWhite_Ubisoft;
}
Text GetFirstPartyLogo(CSystemPlatform _System) {
	return GetFirstPartyLogo(GetPlatform(_System));
}

/* @Changelog

Version 2.0.0
-------------

- All first-party logos are now restricted to pure white (#ffffff) or pure black (#000000) color to match what the C++ enforce (/trackmania-next/tmnext/-/issues/5770)

 */